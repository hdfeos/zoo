;This example code illustrates how to access and visualize GESDISC_MERRA Grid in IDL. 
;If you have any questions, suggestions, comments  on this example, please use the HDF-EOS Forum (http://hdfeos.org/forums). 
;If you would like to see an  example of any other NASA HDF/HDF-EOS data product that is not listed in the HDF-EOS Comprehensive Examples page (http://hdfeos.org/zoo), 
;feel free to contact us at eoshelp@hdfgroup.org or post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; Refer to: MERRA300.prod.assim.tavgU_2d_chm_Fx.200201.hdf
; DataField name: CLDHGH

; IDL example for reading GESDISC MERRA Grid.
; GEO Projection
FILE_NAME='MERRA300.prod.assim.tavgU_2d_chm_Fx.200201.hdf'
file_id = EOS_GD_OPEN(FILE_NAME)

; Define grid name and field
GRID_NAME='EOSGRID'
DATAFIELD_NAME='CLDHGH'

; Retrieve data
grid_id = EOS_GD_ATTACH(file_id, GRID_NAME)
status = EOS_GD_READFIELD(grid_id, DATAFIELD_NAME, data)

; Retrieve fillvalue
status=EOS_GD_GETFILLVALUE(grid_id, DATAFIELD_NAME, fillvalue)

status = EOS_GD_DETACH(grid_id)

; Retrieve latitude and logitude
GRID_NAME='EOSGRID'
DATAFIELD_NAME='XDim'
grid_id = EOS_GD_ATTACH(file_id, GRID_NAME)
status = EOS_GD_READFIELD(grid_id, DATAFIELD_NAME, lon)
status = EOS_GD_DETACH(grid_id)

GRID_NAME='EOSGRID'
DATAFIELD_NAME='YDim'
grid_id = EOS_GD_ATTACH(file_id, GRID_NAME)
status = EOS_GD_READFIELD(grid_id, DATAFIELD_NAME, lat)
status = EOS_GD_DETACH(grid_id)
status = EOS_GD_CLOSE(file_id)

;Convert the M-D data to 2D data at TIME=4
data2D = data[*,*,4]
data2D = Reform(data2D)

; Convert data type
data2D = float(data2D)
fillvalue=float(fillvalue)

;retrieve attributes
FILE_NAME='MERRA300.prod.assim.tavgU_2d_chm_Fx.200201.hdf'
newFileID=HDF_SD_START(FILE_NAME, /READ)

DATAFIELD_NAME='CLDHGH'
index = HDF_SD_NAMETOINDEX(newFileID, DATAFIELD_NAME)

thisSdsID=HDF_SD_SELECT(newFileID, index)

units_index=HDF_SD_ATTRFIND(thisSdsID, 'units')
HDF_SD_ATTRINFO, thisSdsID, units_index, DATA=units

scalefactor_index=HDF_SD_ATTRFIND(thisSdsID, 'scale_factor')
HDF_SD_ATTRINFO, thisSdsID, scalefactor_index, DATA=scale_factor

offset_index=HDF_SD_ATTRFIND(thisSdsID, 'add_offset')
HDF_SD_ATTRINFO, thisSdsID, offset_index, DATA=offset

HDF_SD_END, newFileID

;data transformation 
data2D=offset(0)+(scale_factor(0))*data2D
fillvalue=offset(0)+(scale_factor(0))*fillvalue

;get max and min value of data
idx=where(data2D ne fillvalue(0), cnt)
if cnt gt 0 then datamin=min(data2D[idx])
if cnt gt 0 then datamax=max(data2D[idx])

;Process fill values, convert data that are equal to fillvalue to NaN
idx=where(data2D eq fillvalue, cnt)
if cnt gt 0 then data2D[idx] = !Values.F_NAN

;The following coding is prepared for colorbar. 
;If you require colorbar in your plot, you could download Dr. Fanning's Coyote Library from (http://www.dfanning.com/documents/programs.html). 
;Make a directory named coyote somewhere on your machine, and extract the Coyote files into it. 
;If color bar is not not necessary for your plot, you can ignore this step and make comments ahead of coding. 
;Add the coyote directory you create on your machine to your IDL path. !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH\
!PATH=Expand_Path('+/mnt/hdf/ziying-li/coyote/')+':'+!PATH

; Start off generating the plot
levels = 254
device, decomposed=0
LoadCT, 33, Ncolors=levels, Bottom=1
WINDOW, Title='High-level cloud fraction   at TIME=4'+'  '+'units:'+units, XSIZE=800
MAP_SET, /GRID, /CONTINENTS, XMARGIN=5, YMARGIN=5 ,POSITION=[0.05, 0.05, 0.82, 0.95]
CONTOUR, BYTSCL(data2D, /NAN), lon, lat, /OVERPLOT, /FILL, C_Colors=Indgen(levels)+3, Background=1, NLEVELS=levels, Color=Black
MAP_GRID, /BOX_AXES, COLOR=255
MAP_CONTINENTS, COLOR=255
;The following coding is to generate color bar if your already download and install Dr. Fanning's Coyote Library and add the coyote directory above. 
;If you don't need color bar in your plot, you can ignore this step and make comments ahead of coding.
;Because of the limitation of this routine, the color bar can not display properly in this case.
COLORBAR, Range=[datamin,datamax], Ncolors=levels, /Vertical, Position=[0.9,0.1,0.94,0.9]

