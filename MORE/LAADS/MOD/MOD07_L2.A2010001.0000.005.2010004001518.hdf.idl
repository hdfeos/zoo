;
;  This example code illustrates how to access and visualize LAADS MODIS
;  swath file in IDL. 
;
;  If you have any questions, suggestions, comments on this example, please 
; use the HDF-EOS Forum (http://hdfeos.org/forums). 
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS data 
; product that is not listed in the HDF-EOS Comprehensive Examples page 
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org 
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
;
; Usage:
;                               
; %idl MOD07_L2.A2010001.0000.005.2010004001518.hdf.idl
;
; Tested under: IDL 8.6.0
; Last updated: 2017-11-21

; Open file.
FILE_NAME='MOD07_L2.A2010001.0000.005.2010004001518.hdf'

; Open swath.
SWATH_NAME='mod07'
file_id=EOS_SW_OPEN(FILE_NAME)
swath_id=EOS_SW_ATTACH(file_id, SWATH_NAME)
DATAFIELD_NAME='Total_Ozone'

; Retrieve data field.
status=EOS_SW_READFIELD(swath_id,DATAFIELD_NAME,data)

; Retrieve geo location data field.
status=EOS_SW_READFIELD(swath_id,'Longitude',lon)
status=EOS_SW_READFIELD(swath_id,'Latitude',lat)

; Close file.
status=EOS_SW_DETACH(swath_id)
status=EOS_SW_CLOSE(file_id)

; Get fillvalue, units, scale_factor, and offset attributes.
newFileID=HDF_SD_START(FILE_NAME, /READ)
DATAFIELD_NAME='Total_Ozone'

index=HDF_SD_NAMETOINDEX(newFileID, 'Total_Ozone')

thisSdsID=HDF_SD_SELECT(newFileID, index)

fillvalue_index=HDF_SD_ATTRFIND(thisSdsID, '_FillValue')
HDF_SD_ATTRINFO, thisSdsID, fillvalue_index, DATA=fillvalue

fillvalue=float(fillvalue)

long_name_index=HDF_SD_ATTRFIND(thisSdsID, 'long_name')
HDF_SD_ATTRINFO, thisSdsID, long_name_index, DATA=long_name

units_index=HDF_SD_ATTRFIND(thisSdsID, 'units')
HDF_SD_ATTRINFO, thisSdsID, units_index, DATA=units

scalefactor_index=HDF_SD_ATTRFIND(thisSdsID, 'scale_factor')
HDF_SD_ATTRINFO, thisSdsID, scalefactor_index, DATA=scale_factor

offset_index=HDF_SD_ATTRFIND(thisSdsID, 'add_offset')
HDF_SD_ATTRINFO, thisSdsID, offset_index, DATA=offset

HDF_SD_END, newFileID

; Convert data type to float.
dataf=float(data)

; Process fill values.
idx=where(data eq fillvalue(0), cnt)
if cnt gt 0 then dataf[idx] = !Values.F_NAN

; Apply scale and offset.
dataf=(scale_factor(0))*(dataf-offset(0))

; Draw plot.
m = MAP('Orthographic', TITLE=file_name+'$\n$'+ DATAFIELD_NAME, FONT_SIZE=9,$
        /BUFFER, CENTER_LONGITUDE = MEDIAN(lon),  CENTER_LATITUDE = MEDIAN(lat))

ct = COLORTABLE(72)
t1 = TEXT(0.25, 0.05, FONT_SIZE=9, long_name)
c1 = CONTOUR(dataf, lon, lat, /FILL, OVERPLOT=m, $
             RGB_TABLE=ct, $
             GRID_UNITS='degrees', POSITION=[0.1, 0.1, 0.83, 0.9])
mc = MAPCONTINENTS()
cb = COLORBAR(TARGET=c1, /BORDER, ORIENTATION=1, TEXTPOS=1, $
              POSITION=[0.85,0.2,0.87,0.8], TITLE=units)
png = file_name + '.idl.png'
c1.SAVE, png, HEIGHT=600, WIDTH=800
EXIT


