;  This example code illustrates how to access and visualize Ocean Productivity
;  net primary production (npp) HDF4 file in IDL.
;
;  If you have any questions, suggestions, comments on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums). 
; 
;  If you would like to see an  example of any other NASA HDF/HDF-EOS
; data product that is not listed in the HDF-EOS  Comprehensive  Examples 
; page (http://hdfeos.org/zoo), feel free to contact us at 
; eoshelp@hdfgroup.org or post it at the HDF-EOS Forum 
; (http://hdfeos.org/forums).

; Open file.
file_name='npp.2010361.hdf'

newFileID=HDF_SD_START(file_name, /READ)

; Get vector point for lat and lon
index=HDF_SD_ATTRFIND(newFileID, 'Start Time String')
HDF_SD_ATTRINFO, newFileID, index, DATA=start_time_string

index=HDF_SD_ATTRFIND(newFileID, 'Stop Time String')
HDF_SD_ATTRINFO, newFileID, index, DATA=stop_time_string

; Retrieve data.
; See [1] for the meaningful description of data set.
datafield_name="npp"
index=HDF_SD_NAMETOINDEX(newFileID, datafield_name)

thisSdsID=HDF_SD_SELECT(newFileID, index)
HDF_SD_GETDATA, thisSdsID, data

; Read the Units attribute.
index=HDF_SD_ATTRFIND(thisSdsID, 'Units')
HDF_SD_ATTRINFO, thisSdsID, index, DATA=units

; Read the Hole Value attribute.
index=HDF_SD_ATTRFIND(thisSdsID, 'Hole Value')
HDF_SD_ATTRINFO, thisSdsID, index, DATA=fillvalue

; Close file.
HDF_SD_END, newFileID

; Set lat / lon variable based on FAQ [2].
nlat = 2160
nlon = 4320

lat=90.0 - (180.0/nlat)* (FINDGEN(nlat) + 0.5)
lon= -180.0 + (360.0/nlon)*(FINDGEN(nlon) + 0.5)


; Process fill values, convert data that are equal to fillvalue to NaN.
idx=WHERE(data eq fillvalue(0), cnt)
IF cnt GT 0 THEN data[idx] = !Values.F_NAN

; The max value goes up to 13K. Limit the value to get a good plot like [3].
idx=WHERE(data gt 1001, cnt)
IF cnt GT 0 THEN data[idx] = 1000

; Get max and min value of data for contour and color bar.
datamin = MIN(data, /NAN)
datamax = MAX(data, /NAN)

; Generate the plot.
levels=254
DEVICE, DECOMPOSED=0
LOADCT,33, NCOLOR=levels, BOTTOM=1
WINDOW, TITLE='FIELD:' + datafield_name + '  ' + 'UNIT:' + units, XSIZE=800, YSIZE=500
MAP_SET, /GRID, /CONTINENTS, XMARGIN=5, YMARGIN=5, POSITION=[0.05, 0.05, 0.81, 0.82], /NOERASE

CONTOUR, data, lon, lat, /OVERPLOT, /CELL_FILL, C_COLORS=Indgen(levels)+3, BACKGROUND=1, NLEVELS=levels, COLOR=Black, MIN_VALUE=datamin, MAX_VALUE=datamax
MAP_GRID, /BOX_AXES, COLOR=255
MAP_CONTINENTS, COLOR=255

; Draw file name, field name, and unit.
XYOUTS, 0.05, 0.87, /NORMAL, 'FIELD:' + datafield_name  +  $ 
       " (" + start_time_string + "  - " $
        +  stop_Time_string + ")", $
        CHARSIZE=1.25,  ALIGNMENT=0.0
XYOUTS, 0.94, 0.87, /NORMAL, 'UNIT:' + units, $ 
  CHARSIZE=1.25,  ALIGNMENT=1.0
XYOUTS, 0.43, 0.92, /NORMAL, file_name, $
  CHARSIZE=1.75,  ALIGNMENT=0.5

; The following code is prepared for colorbar. 
;
; If you want to have a color bar in your plot, please download 
; "Dr. Fanning's Coyote Library" from [4]. Make a directory named
; coyote somewhere on your machine, and extract the Coyote library files into
; it. 
;
; If color bar is not not necessary for your plot, you can ignore this
; step and add comment character ';' at the beginning of the rest of codes.
;
; Add the coyote directory that you created on your machine to your IDL
; path like below: 
;
; !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH

; We assume that the coyote library is installed under the current working
; directory that this code exists.
!PATH=EXPAND_PATH('+coyote/')+':'+!PATH

; The following code assumes that you've already download and installed 
; "Dr. Fanning's Coyote Library" and add the coyote directory above. 
;
; If you don't need a color bar in your plot, you can ignore this step
; by adding comment character ';' at the beginning of the code.
COLORBAR, RANGE=[datamin, datamax], NCOLORS=levels, /VERTICAL, $
  Position=[0.94,0.05,0.98,0.82]


; Write JPEG image file.
im = TVRD(TRUE=3)
jpg = file_name + '.idl.JPG'
print, jpg   
WRITE_JPEG, jpg, im, QUALITY=100, TRUE=3

; Reference
;
; [1]http://orca.science.oregonstate.edu/2160.by.4320.8day.hdf.vgpm.m.chl.m.sst4.php
; [2] http://orca.science.oregonstate.edu/faq01.php
; [3] http://www.science.oregonstate.edu/ocean.productivity/standard.product.php
; [4] http://www.dfanning.com/documents/programs.html

