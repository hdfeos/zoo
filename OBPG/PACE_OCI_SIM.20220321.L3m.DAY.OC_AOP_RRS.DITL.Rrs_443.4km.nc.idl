;
;  This example code illustrates how to access and visualize 
;  OBPG PACE Grid netCDF-4 file in IDL.
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
;
; Usage:
;
;  $idl PACE_OCI_SIM.20220321.L3m.DAY.OC_AOP_RRS.DITL.Rrs_443.4km.nc.idl
;
; Tested under: IDL 8.8.3
; Last updated: 2023-10-26

; Open file. 
file_name='PACE_OCI_SIM.20220321.L3m.DAY.OC_AOP_RRS.DITL.Rrs_443.4km.nc'
file_id=NCDF_OPEN(file_name)

; Read data.
datafield_name='Rrs_443'
data_id=NCDF_VARID(file_id, datafield_name)

; Retrieve data.
NCDF_VARGET, file_id, data_id, data

; Get long_name.
NCDF_ATTGET, file_id, data_id, 'long_name', long_name

; Get units.
NCDF_ATTGET, file_id, data_id, 'units', units

; Get _FillValue.
NCDF_ATTGET, file_id, data_id, '_FillValue', fillvalue

; Get scale.
NCDF_ATTGET, file_id, data_id, 'scale_factor', scale

; Get offset.
NCDF_ATTGET, file_id, data_id, 'add_offset', offset


; Read latitude.
latitude_name='lat'
latitude_id=NCDF_VARID(file_id, latitude_name)
NCDF_VARGET, file_id, latitude_id, lat

; Read longitude.
lon_name='lon'
longitude_id=NCDF_VARID(file_id, lon_name)
NCDF_VARGET, file_id, longitude_id, lon

; Close file.
NCDF_CLOSE, file_id

; Convert type to double.
dataf = float(data)

; Process fill value.
idx=where(data eq fillvalue(0), cnt)
if cnt gt 0 then dataf[idx] = !Values.F_NAN

; Process scale/offset.
dataf = scale * dataf + offset

; Generate the plot.
m = MAP('Geographic', TITLE=file_name, FONT_SIZE=9, /BUFFER)
ct = COLORTABLE(72, /reverse)
t1 = TEXT(0.35, 0.1, STRING(long_name))
c1 = CONTOUR(dataf, lon, lat, OVERPLOT=m, $
             /FILL, $
             RGB_INDICES=[0,30,60,90,120,150,220,230,240,255], $
             C_VALUE = [0.01, 0.02, 0.05, 0.1, 0.2, 0.5, 1, 2, 5, 7], $
             RGB_TABLE=ct, $
             GRID_UNITS='degrees', POSITION=[0.1, 0.1, 0.83, 0.9])
mc = MAPCONTINENTS()
datamin=MIN(dataf)
datamax=MAX(dataf)
cb = COLORBAR(TARGET=c1, /BORDER, ORIENTATION=1, TEXTPOS=1,  $
              Position=[0.85,0.2,0.87,0.8], TITLE=STRING(units))
png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT
