;
;  This example code illustrates how to access and visualize OBPG VIIRS L2
; HDF4 file in IDL. 
;
;  If you have any questions, suggestions, comments on this example, please use
; the HDF-EOS Forum (http://hdfeos.org/forums). 
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS data 
; product that is not listed in the HDF-EOS Comprehensive Examples page 
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org or 
; post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; Usage:
;
;  %idl V2013335180706.L2_NPP_OC.hdf.idl
;
; Tested under: IDL 8.2.3
; Last updated: 2013-12-10

; Open file.
  file_name='V2013335180706.L2_NPP_OC.hdf'
  newFileID=HDF_SD_START(file_name, /READ)

; Define datafield.
  datafield_name="chlor_a"
  index=HDF_SD_NAMETOINDEX(newFileID,datafield_name)

; Retrieve data.
  thisSdsID=HDF_SD_SELECT(newFileID, index)
  HDF_SD_GETDATA, thisSdsID, data

; Retrieve long_name, units, and bad_value_scaled attributes.
; Slope and intercept attributes are not retrieved
; because they are 1.0 and 0.0 respectively.
  long_name_index=HDF_SD_ATTRFIND(thisSdsID, 'long_name')
  HDF_SD_ATTRINFO, thisSdsID, long_name_index, DATA=long_name

  units_index=HDF_SD_ATTRFIND(thisSdsID, 'units')
  HDF_SD_ATTRINFO, thisSdsID, units_index, DATA=units

  bad_value_index=HDF_SD_ATTRFIND(thisSdsID, 'bad_value_scaled')
  HDF_SD_ATTRINFO, thisSdsID, bad_value_index, DATA=fillvalue

; Read lon/lat information.
  lon_name="longitude"
  index=HDF_SD_NAMETOINDEX(newFileID,lon_name)
  thisSdsID=HDF_SD_SELECT(newFileID, index)
  HDF_SD_GETDATA, thisSdsID, lon

  lat_name="latitude"
  index=HDF_SD_NAMETOINDEX(newFileID,lat_name)
  thisSdsID=HDF_SD_SELECT(newFileID, index)
  HDF_SD_GETDATA, thisSdsID, lat

; Close file
  HDF_SD_END, newFileID

; Process fill values.
  idx=where(data eq fillvalue(0), cnt)
  IF cnt GT 0 THEN data[idx] = !Values.F_NAN

; Get max and min value of data for color bar.
  datamin = MIN(data, /NAN)
  datamax = MAX(data, /NAN)


; Get vertex value for lat and lon
  latmin=MIN(lat)
  latmax=MAX(lat)
  lonmin=MIN(lon)
  lonmax=MAX(lon)

; Generate the plot.
  SET_PLOT, 'Z'
  levels=12
  DEVICE, SET_RESOLUTION=[800,600], SET_PIXEL_DEPTH=24, DECOMPOSED=0
  LOADCT,33, NCOLORS=levels, BOTTOM=1
  MAP_SET, /GRID, /CONTINENTS, /HIRES, XMARGIN=5, YMARGIN=5, $
           POSITION=[0.05, 0.05, 0.85, 0.82], /NOERASE, $
           LIMIT=[latmin, lonmin, latmax, lonmax]    

; Most data values fall into 0.0-1.0. 
  CONTOUR, data, lon, lat, /OVERPLOT, /CELL_FILL, C_COLORS=Indgen(levels)+3, $
           LEVELS=[0.0, 0.1, 0.2, 0.3, 0.4, 0.5, 1.0, 2.0, 4.0, 8.0, 16.0, 32.0]
  MAP_GRID, /BOX_AXES
  MAP_CONTINENTS, /HIRES

; Draw file name, field name, and unit.
  XYOUTS, 0.05, 0.87, /NORMAL, 'FIELD:' + long_name, $
    CHARSIZE=1.25,  ALIGNMENT=0.0
  XYOUTS, 0.94, 0.87, /NORMAL, 'UNIT:' + units, $ 
    CHARSIZE=1.25,  ALIGNMENT=1.0
  XYOUTS, 0.43, 0.92, /NORMAL, file_name, $
    CHARSIZE=1.75,  ALIGNMENT=0.5

; The following code is prepared for colorbar. 
;
; If you want to have a color bar in your plot, please download 
; "Dr. Fanning's Coyote Library" from [1]. Make a directory named
; coyote somewhere on your machine, and extract the Coyote library files into
; it. 
;
; If color bar is not not necessary for your plot, you can ignore this
; step and add comment character ';' at the beginning of the rest of codes.
;
; Add the coyote directory that you created on your machine to your IDL
; path like below: 
;
; !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH

; We assume that the coyote library is installed under the current working
; directory that this code exists.
  !PATH=EXPAND_PATH('+coyote/')+':'+!PATH

; The following code assumes that you've already download and installed 
; "Dr. Fanning's Coyote Library" and add the coyote directory above. 
;
; If you don't need a color bar in your plot, you can ignore this step
; by adding comment character ';' at the beginning of the code.
  DCBAR, BOTTOM=1, NCOLORS=levels,  /VERTICAL, Position=[0.9,0.05,0.94,0.82], $
         LABELS=['0.0', '0.1', '0.2', '0.3', '0.4', '0.5', '1.0', '2.0', '4.0', '8.0', '16.0', '32.0+']

; Write PNG image file.
  im = TVRD(TRUE=1)
  WRITE_PNG, file_name+'.idl.png', im
  EXIT

; Reference
;
; [1] http://www.dfanning.com/documents/programs.html
