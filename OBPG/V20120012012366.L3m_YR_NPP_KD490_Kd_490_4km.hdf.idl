;
;  This example code illustrates how to access and visualize OBPG VIIRS Grid
; HDF4 file in IDL. 
;
;  If you have any questions, suggestions, comments on this example, please use
; the HDF-EOS Forum (http://hdfeos.org/forums). 
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS data 
; product that is not listed in the HDF-EOS Comprehensive Examples page 
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org or 
; post it at the HDF-EOS Forum (http://hdfeos.org/forums).


; Usage:
;
;  %idl V20120012012366.L3m_YR_NPP_KD490_Kd_490_4km.hdf.idl
;
; Tested under: IDL 8.2.3
; Last updated: 2013-12-11

; Open file.
  file_name='V20120012012366.L3m_YR_NPP_KD490_Kd_490_4km.hdf'
  newFileID=HDF_SD_START(file_name, /READ)

; Get vector point for lat and lon.
  index=HDF_SD_ATTRFIND(newFileID, 'SW Point Latitude')
  HDF_SD_ATTRINFO, newFileID, index, DATA=veclat

  index=HDF_SD_ATTRFIND(newFileID, 'SW Point Longitude')
  HDF_SD_ATTRINFO, newFileID, index, DATA=veclon

; Get vector point for lat step and lon step.
  index=HDF_SD_ATTRFIND(newFileID, 'Latitude Step')
  HDF_SD_ATTRINFO, newFileID, index, DATA=latstep

  index=HDF_SD_ATTRFIND(newFileID, 'Longitude Step')
  HDF_SD_ATTRINFO, newFileID, index, DATA=lonstep

; Get the data field description.
  index=HDF_SD_ATTRFIND(newFileID, 'Parameter')
  HDF_SD_ATTRINFO, newFileID, index, DATA=long_name

  index=HDF_SD_ATTRFIND(newFileID, 'Units')
  HDF_SD_ATTRINFO, newFileID, index, DATA=units

; Define data field.
  datafield_name="l3m_data"
  index=HDF_SD_NAMETOINDEX(newFileID,datafield_name)

; Retrieve data.
  thisSdsID=HDF_SD_SELECT(newFileID, index)
  HDF_SD_GETDATA, thisSdsID, data

; Read fill value. 
; This product has scale=1 and offset=0 so we will not read those attributes.
  index=HDF_SD_ATTRFIND(thisSdsID, 'Fill')
  HDF_SD_ATTRINFO, thisSdsID, index, DATA=fillvalue

; Close file
  HDF_SD_END, newFileID

; Get the dimension size of data.
  dimsize=SIZE(data, /DIM)
  numlat=dimsize(1)
  numlon=dimsize(0)

; Calculate the latitude and longitude range based on vector points
; and lat/lon step
  lat=FINDGEN(numlat)*latstep(0)+veclat(0)
  lon=FINDGEN(numlon)*lonstep(0)+veclon(0)

; Reverse lat
  lat=reverse(lat)

; Convert data type
  dataf=float(data)

; Process fill values, convert data that are equal to fillvalue to NaN
  idx=where(data eq fillvalue(0), cnt)
  IF cnt GT 0 THEN dataf[idx] = !Values.F_NAN


; Get max and min value of data for color bar.
  datamin = MIN(dataf, /NAN)
  datamax = MAX(dataf, /NAN)

; Generate the plot.
  SET_PLOT, 'Z'
  levels=10
  DEVICE, SET_RESOLUTION=[800,600], SET_PIXEL_DEPTH=24, DECOMPOSED=0
  LOADCT,33, NCOLORS=levels, BOTTOM=1
  MAP_SET, /GRID, /CONTINENTS, /HIRES, XMARGIN=5, YMARGIN=5, $
           POSITION=[0.05, 0.05, 0.85, 0.82], /NOERASE

; Use levels similar to OBPG website example [3].
  CONTOUR, dataf, lon, lat, /OVERPLOT, /CELL_FILL, C_COLORS=Indgen(levels)+3, $
           LEVELS=[0.01, 0.02, 0.05, 0.1, 0.2, 0.5, 1.0, 2.0, 5.0, 7.0]
  MAP_GRID, /BOX_AXES
  MAP_CONTINENTS, /HIRES

; Draw file name, field name, and unit.
  XYOUTS, 0.05, 0.87, /NORMAL, 'FIELD:' + long_name, $
    CHARSIZE=1.25,  ALIGNMENT=0.0
  XYOUTS, 0.94, 0.87, /NORMAL, 'UNIT:' + units, $ 
    CHARSIZE=1.25,  ALIGNMENT=1.0
  XYOUTS, 0.43, 0.92, /NORMAL, file_name, $
    CHARSIZE=1.75,  ALIGNMENT=0.5

; The following code is prepared for colorbar. 
;
; If you want to have a color bar in your plot, please download 
; "Dr. Fanning's Coyote Library" from [2]. Make a directory named
; coyote somewhere on your machine, and extract the Coyote library files into
; it. 
;
; If color bar is not not necessary for your plot, you can ignore this
; step and add comment character ';' at the beginning of the rest of codes.
;
; Add the coyote directory that you created on your machine to your IDL
; path like below: 
;
; !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH

; We assume that the coyote library is installed under the current working
; directory that this code exists.
  !PATH=EXPAND_PATH('+coyote/')+':'+!PATH

; The following code assumes that you've already download and installed 
; "Dr. Fanning's Coyote Library" and add the coyote directory above. 
;
; If you don't need a color bar in your plot, you can ignore this step
; by adding comment character ';' at the beginning of the code.
  DCBAR, BOTTOM=1, NCOLORS=levels,  /VERTICAL, Position=[0.9,0.05,0.94,0.82], $
         LABELS=['0.01', '0.02', '0.05', '0.1', '0.2', '0.5', '1.0', '2.0', '5.0', '7.0']

; Write PNG image file.
  im = TVRD(TRUE=1)
  WRITE_PNG, file_name+'.idl.png', im
  EXIT

; Reference
;
;
; [1] http://oceancolor.gsfc.nasa.gov/cgi/l3
; [2] http://www.dfanning.com/documents/programs.html
