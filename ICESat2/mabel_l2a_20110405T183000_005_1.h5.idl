;  This example code illustrates how to access and visualize ICESAT-2 MABEL
; L2 HDF5 file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
; If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; Tested under: IDL 8.0
; Last updated: 2012-05-14

; Open file.
file_name='mabel_l2a_20110405T183000_005_1.h5' ;
file_id=H5F_OPEN(file_name)

; Read latitude.
latitude_name='/photon/channel001/latitude'
latitude_id=H5D_OPEN(file_id, latitude_name)
lat=H5D_READ(latitude_id)

; Close dataset.
H5D_CLOSE, latitude_id

; Read longitude.
lon_name='/photon/channel001/longitude'
lon_id=H5D_OPEN(file_id, lon_name)
lon=H5D_READ(lon_id)

; Close dataset.
H5D_CLOSE, lon_id


; Read elevation.
lev_name='/photon/channel001/elev'
lev_id=H5D_OPEN(file_id, lev_name)
lev=H5D_READ(lev_id)

; Read unit attribute.
units_lev_id=H5A_OPEN_NAME(lev_id, 'units')
units_lev=H5A_READ(units_lev_id)
H5A_CLOSE, units_lev_id

; Read long name attribute.
ln_lev_id=H5A_OPEN_NAME(lev_id, 'long_name')
long_name_lev=H5A_READ(ln_lev_id)
H5A_CLOSE, ln_lev_id


; Close dataset.
H5D_CLOSE, lev_id

; Read delta time.
time_name='/photon/channel001/delta_time'
time_id=H5D_OPEN(file_id, time_name)
time=H5D_READ(time_id)

; Close dataset.
H5D_CLOSE, time_id

; Close file.
H5F_CLOSE, file_id

; Create an elapsed time variable.
time_e = time - time(0)

; Generate the plot.
levels = 254
DEVICE, DECOMPOSED=0
LOADCT, 33, NCOLORS=levels, BOTTOM=1

; Plot graphs.
WINDOW, Title=file_name, XSIZE=800, YSIZE=400*2 

; Plot elapsed time vs elevation.
;
; Without the /NOERASE option, the previous plot will be erased.
;
; Use subset like below to see the real elevation change.
;
dimsize = SIZE(time_e, /DIMENSIONS)
count = dimsize(0) - 1;
npoints = 100
step = dimsize(0) / npoints
PLOT, time_e[0:count:step], lev[0:count:step], POSITION=[0.1, 0.52,  0.93, 0.94], $
  XTITLE='ElapsedTime (sec)', /NOERASE
XYOuts, 0.1, 0.96, /NORMAL, 'FIELD:' + long_name_lev, $
        CHARSIZE=1.25,  ALIGNMENT=0.0
XYOuts, 0.93, 0.96, /NORMAL, 'UNIT:' + units_lev, $ 
        CHARSIZE=1.25,  ALIGNMENT=1.0
XYOuts, 0.5, 0.98, /NORMAL, file_name + '!C 100 sample points of channel001', $
        CHARSIZE=1.75,  ALIGNMENT=0.5

; Get corner values for lat and lon.
lonmin=MIN(lon)
lonmax=MAX(lon)
latmin=MIN(lat)
latmax=MAX(lat)

; Plot the trajectory on map.
MAP_SET, /GRID, /CONTINENTS, XMARGIN=5, YMARGIN=5, $
         POSITION=[0.1, 0.05, 0.45, 0.4], $ 
         LIMIT=[latmin, lonmin, latmax, lonmax], /NOERASE 

MAP_GRID, /BOX_AXES, COLOR=255
MAP_CONTINENTS, COLOR=255
OPLOT, lon[0:count:step], lat[0:count:step]
PLOTS, lon(0), lat(0), COLOR=250, PSYM=1, SYMSIZE=2, THICK=2
XYOUTS, 0.1, 0.44, /NORMAL, 'Trajectory of Flight Path', $
        CHARSIZE=1.75, COLOR=255, ALIGNMENT=0.0

; Plot the location on global map.
MAP_SET, /GRID, /CONTINENTS, XMARGIN=5, YMARGIN=5, $
         POSITION=[0.5, 0.05, 0.93, 0.4],  /NOERASE 
MAP_GRID, /BOX_AXES, COLOR=255
MAP_CONTINENTS, COLOR=255
PLOTS, lon(0), lat(0), COLOR=250, PSYM=1, SYMSIZE=2, THICK=2
XYOUTS, 0.9, 0.44, /NORMAL, 'Starting Location of Flight Path', $
        CHARSIZE=1.75, COLOR=255, ALIGNMENT=1.0

; Write JPEG image file.
im = TVRD(TRUE=3)
jpg = file_name + '.idl.jpg'   
WRITE_JPEG, jpg, im, QUALITY=100, TRUE=3
