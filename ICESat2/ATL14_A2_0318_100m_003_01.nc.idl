;
;  This example code illustrates how to access and visualize
;  a ICESat-2 ATL14 netCDF-4/HDF5 Grid file in IDL.
;
; If you have any questions, suggestions, comments  on this example,
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
;
; Usage:
;
;  $idl ATL14_AA_0314_100m_002_02.nc.idl
;
; Tested under: IDL 8.9.0
; Last updated: 2024-04-02
;
; Credit: NV5 Geospatial Solutions Technical Support
;

file_name = 'ATL14_A2_0318_100m_003_01.nc'

; Open file.
file_id=NCDF_OPEN(file_name)
    
; Read data.
datafield_name='h'
data_id=NCDF_VARID(file_id, datafield_name)
    
; Retrieve data.
NCDF_VARGET, file_id, data_id, data, $
             COUNT=[268, 228], OFFSET=[0,0], STRIDE=[100, 100]
; PRINT, SIZE(data)
    
; Get long_name.
NCDF_ATTGET, file_id, data_id, 'long_name', long_name
long_name = STRING(long_name)

; Get units.
NCDF_ATTGET, file_id, data_id, 'units', units
units = STRING(units)
    
; Get _FillValue.
NCDF_ATTGET, file_id, data_id, '_FillValue', fillvalue
    
; Read latitude.
latitude_name='y'
latitude_id=NCDF_VARID(file_id, latitude_name)
NCDF_VARGET, file_id, latitude_id, y
y = y[0:*:100]
; PRINT, SIZE(y)
    
; Read longitude.
lon_name='x'
longitude_id=NCDF_VARID(file_id, lon_name)
NCDF_VARGET, file_id, longitude_id, x
x = x[0:*:100]
; PRINT, SIZE(x)
    
; Close file.
NCDF_CLOSE, file_id
    
; Process fill value.
idx=where(data eq fillvalue(0), cnt)
if cnt gt 0 then data[idx] = !Values.F_NAN
datamin = min(data, /NAN)
datamax = max(data, /NAN)
     
m = MAP('Polar Stereographic', $
        LIMIT=[-90, -180, -60, 180], $
        CENTER_LATITUDE=-90.0, $
        TRUE_SCALE_LATITUDE=-71, $
        ELLIPSOID='WGS 84', $
        POSITION=[0.1, 0.1, 0.83, 0.9],$
        TITLE=file_name,/BUFFER)
    
ct = COLORTABLE(72, /reverse)

t1 = TEXT(0.35, 0.01, long_name)
im = IMAGE(data, x, y, grid_units='meters', /overplot, RGB_TABLE=ct)
mc = MAPCONTINENTS()
cb = COLORBAR(RGB_TABLE=ct, RANGE=[datamin, datamax], /BORDER, ORIENTATION=1, $
              TEXTPOS=1, POSITION=[0.85,0.2,0.87,0.8], TITLE=units)
png = file_name + '.idl.png'
im.save, png, HEIGHT=600, WIDTH=800
EXIT
