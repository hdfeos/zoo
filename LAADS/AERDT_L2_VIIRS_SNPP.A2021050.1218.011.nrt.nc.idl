;
;  This example code illustrates how to access and visualize LAADS
;  AERDT_L2_VIIRS_SNPP netCDF-4/HDF5 Swath file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
; 
; Usage:
;                               
; $idl AERDT_L2_VIIRS_SNPP.A2021050.1218.011.nrt.nc.idl
;
; Tested under: IDL 8.7.2
; Last updated: 2021-03-03

; Open file.
file_name='AERDT_L2_VIIRS_SNPP.A2021050.1218.011.nrt.nc'
file_id=H5F_OPEN(file_name)

; Read dataset.
data_name='/geophysical_data/Image_Optical_Depth_Land_And_Ocean'
data_id=H5D_OPEN(file_id, data_name)
data=H5D_READ(data_id)

; Get 'long_name' attribute.
title_id=H5A_OPEN_NAME(data_id, 'long_name')
long_name=H5A_READ(title_id)
H5A_CLOSE, title_id

; Get 'units' attribute.
units_id=H5A_OPEN_NAME(data_id, 'units')
units=H5A_READ(units_id)
H5A_CLOSE, units_id

; Get scale_factor attribute.
scale_factor_id = H5A_OPEN_NAME(data_id, 'scale_factor')
scale_factor = H5A_READ(scale_factor_id)
H5A_CLOSE, scale_factor_id

; Get add_offset attribute.
add_offset_id = H5A_OPEN_NAME(data_id, 'add_offset')
add_offset = H5A_READ(add_offset_id)
H5A_CLOSE, add_offset_id

; Get missing value attribute.
fillvalue_id = H5A_OPEN_NAME(data_id, '_FillValue')
fillvalue = H5A_READ(fillvalue_id)
H5A_CLOSE, fillvalue_id
H5D_CLOSE, data_id

; Read latitude.
lat_name='/geolocation_data/latitude'
lat_id=H5D_OPEN(file_id, lat_name)
lat=H5D_READ(lat_id)
H5D_CLOSE, lat_id

; Read longitude.
lon_name='/geolocation_data/longitude'
lon_id=H5D_OPEN(file_id, lon_name)
lon=H5D_READ(lon_id)
H5D_CLOSE, lon_id
H5F_CLOSE, file_id

; Convert data type.
dataf = float(data)

; Process missing value, convert dataf that are equal to fillvalue to NaN
idx = where(data eq fillvalue(0), cnt)
if cnt gt 0 then dataf[idx] = !Values.F_NAN
dataf=(scale_factor(0))*(dataf)+add_offset(0)


; Get max and min value of data for color bar.
datamin = MIN(dataf, /NAN)
datamax = MAX(dataf, /NAN)

; Get min/max value of lat and lon for zoomed image.
latmin=MIN(lat)
latmax=MAX(lat)
lonmin=MIN(lon)
lonmax=MAX(lon)

; Pick only valid points for scatter plot.
gidx = WHERE(FINITE(dataf), count)
lats = lat[gidx]
lons = lon[gidx]
datas = dataf[gidx]
datamin = MIN(datas)
datamax = MAX(datas)

; Use LIMIT for zoom-in effect.
m = MAP('Geographic', LIMIT=[latmin, lonmin, latmax, lonmax], $
        TITLE=file_name, FONT_SIZE=9, /BUFFER)
; This color table has white color for fill value.
ct = COLORTABLE(13)
t1 = TEXT(0.01, 0.05, FONT_SIZE=8, long_name)
c1 = SCATTERPLOT(lons[*], lats[*], OVERPLOT=m, $
                  MAGNITUDE = datas[*], $
                  RGB_TABLE=ct, $
                  POSITION=[0.1, 0.1, 0.83, 0.9],$
                  /SYM_FILLED, SYMBOL='o', SYM_SIZE=0.1)
mc = MAPCONTINENTS()
cb = COLORBAR(RGB_TABLE=ct, RANGE=[datamin, datamax], /BORDER, ORIENTATION=1, $
              TEXTPOS=1, POSITION=[0.85,0.2,0.87,0.8], TITLE=units)

png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT
