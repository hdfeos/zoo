;
;  This example code illustrates how to access and visualize LAADS
;  VNP14IMG v2 netCDF-4/HDF5 Swath file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
; 
; Usage:
;                               
; $idl VNP14IMG.A2018064.1200.002.2024079084304.nc.idl
;
; Tested under: IDL 8.9.0
; Last updated: 2024-08-01

; Open file.
file_name='VNP14IMG.A2018064.1200.002.2024079084304.nc'
file_id=H5F_OPEN(file_name)

; Read FP_T5 data.
data_name='FP_T5'
data_id=H5D_OPEN(file_id, data_name)
data=H5D_READ(data_id)

; Get 'long_name' attribute.
title_id=H5A_OPEN_NAME(data_id, 'long_name')
long_name=H5A_READ(title_id)
H5A_CLOSE, title_id

; Get 'units' attribute.
units_id=H5A_OPEN_NAME(data_id, 'units')
units=H5A_READ(units_id)
H5A_CLOSE, units_id
H5D_CLOSE, data_id

; Read latitude.
lat_name='FP_latitude'
lat_id=H5D_OPEN(file_id, lat_name)
lat=H5D_READ(lat_id)
H5D_CLOSE, lat_id

; Read longitude.
lon_name='FP_longitude'
lon_id=H5D_OPEN(file_id, lon_name)
lon=H5D_READ(lon_id)
H5D_CLOSE, lon_id
H5F_CLOSE, file_id

; Get max and min value of data for color bar.
datamin = MIN(data)
datamax = MAX(data)

; Set center lat/lon point for Ortho map.
dimsize=SIZE(data,/dim)
numlat=dimsize(0)
c_lon = lon[numlat/2]
c_lat = lat[numlat/2]

; Get min/max value of lat and lon for zoomed image.
; latmin=MIN(lat)
; latmax=MAX(lat)
; lonmin=MIN(lon)
; lonmax=MAX(lon)
;
; Use LIMIT for zoom-in effect.
;   MAP(..., LIMIT=[latmin, lonmin, latmax, lonmax], ...)
m = MAP('Orthographic', TITLE=file_name, FONT_SIZE=9, /BUFFER, $
        CENTER_LONGITUDE = c_lon,  CENTER_LATITUDE = c_lat)

ct = COLORTABLE(72, /reverse)
t1 = TEXT(0.01, 0.05, FONT_SIZE=8, long_name)
c1 = SCATTERPLOT(lon, lat, OVERPLOT=m, $
                 MAGNITUDE = data, $
                 RGB_TABLE=ct, $
                 POSITION=[0.1, 0.1, 0.83, 0.9],$
                 /SYM_FILLED, SYMBOL='o', SYM_SIZE=0.1)
mc = MAPCONTINENTS()
cb = COLORBAR(TARGET=c1, /BORDER, ORIENTATION=1, TEXTPOS=1,$
              RANGE=[datamin, datamax], POSITION=[0.85,0.2,0.87,0.8], TITLE=units)
png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT
