
;  This example code illustrates how to access and visualize
;  GES DISC TROPICS06 L2 netCDF-4/HDF5 file in IDL.
;
; If you have any questions, suggestions, comments  on this example,
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
;
; Usage:
;
;  $idl TROPICS06.MIRS.L2B.Orbit04435.V02-06.ST20240224-162719.ET20240224-180211.CT20240325-020618.nc.idl
;
; Tested under: IDL 8.9.0
; Last updated: 2024-05-31

; Open file. 
file_name='TROPICS06.MIRS.L2B.Orbit04435.V02-06.ST20240224-162719.ET20240224-180211.CT20240325-020618.nc'
file_id=NCDF_OPEN(file_name)

; Read data.
datafield_name='TPW'
data_id=NCDF_VARID(file_id, datafield_name)

; Retrieve data.
NCDF_VARGET, file_id, data_id, data

; Get long_name.
NCDF_ATTGET, file_id, data_id, 'long_name', long_name

; Get units.
NCDF_ATTGET, file_id, data_id, 'units', units

; Get _FillValue.
NCDF_ATTGET, file_id, data_id, '_FillValue', fillvalue

; Get scale.
NCDF_ATTGET, file_id, data_id, 'scale_factor', scale

; Read latitude.
latitude_name='Latitude'
latitude_id=NCDF_VARID(file_id, latitude_name)
NCDF_VARGET, file_id, latitude_id, lat

; Read longitude.
lon_name='Longitude'
longitude_id=NCDF_VARID(file_id, lon_name)
NCDF_VARGET, file_id, longitude_id, lon

; Close file.
NCDF_CLOSE, file_id

; Convert type to float.
dataf = float(data)

; Process fill value.
idx = WHERE(data eq fillvalue(0), cnt)
if cnt gt 0 then dataf[idx] = !Values.F_NAN

; Process scale/offset.
dataf = scale * dataf

datamin = MIN(dataf, /NAN)
datamax = MAX(dataf, /NAN)

; lat/lon has fill values. Filter them to get correct output image.
idx = WHERE(data NE fillvalue(0))

; Generate the plot.
m = MAP('Geographic', TITLE=file_name, FONT_SIZE=6, /BUFFER)
ct = COLORTABLE(72, /reverse)
c1 = SCATTERPLOT(lon[idx], lat[idx], OVERPLOT=m, $
                 MAGNITUDE=dataf[idx], $
                 RGB_TABLE=ct, $
                 POSITION=[0.1, 0.1, 0.83, 0.9],$
                 /SYM_FILLED, SYMBOL='o', SYM_SIZE=0.1)
mc = MAPCONTINENTS()
t1 = TEXT(0.01, 0.05, FONT_SIZE=8, STRING([long_name]))
cb = COLORBAR(RGB_TABLE=ct, RANGE=[datamin, datamax], ORIENTATION=1, /BORDER,$
              TEXTPOS=1, POSITION=[0.85,0.2,0.87,0.8], TITLE=STRING([units]))
grid = m.MAPGRID
grid.LABEL_POSITION = 0

png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT
