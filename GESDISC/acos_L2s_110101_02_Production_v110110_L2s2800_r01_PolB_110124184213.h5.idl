;  This example code illustrates how to access and visualize GES DISC GOSAT 
; ACOS L2 Swath HDF5 file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
; If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; Tested under: IDL 8.0
; Last updated: 2011-11-21

; Open file.
file_name='acos_L2s_110101_02_Production_v110110_L2s2800_r01_PolB_110124184213.h5';
file_id=H5F_OPEN(file_name)

; Read data.
datafield_name='/RetrievalResults/xco2'
data_id=H5D_OPEN(file_id,datafield_name)
data=H5D_READ(data_id)

; Read attribute.
units_id=H5A_OPEN_NAME(data_id, 'Units')
units=H5A_READ(units_id)
H5A_CLOSE, units_id


; Close dataset.
H5D_CLOSE, data_id

; Read latitude.
latitude_name='/SoundingGeometry/sounding_latitude_geoid'
latitude_id=H5D_OPEN(file_id, latitude_name)
lat=H5D_READ(latitude_id)

; Close dataset.
H5D_CLOSE, latitude_id

; Read longitude.
lon_name='/SoundingGeometry/sounding_longitude_geoid'
lon_id=H5D_OPEN(file_id, lon_name)
lon=H5D_READ(lon_id)

; Close dataset.
H5D_CLOSE, lon_id


; Read altitude.
lev_name='/SoundingGeometry/sounding_altitude'
lev_id=H5D_OPEN(file_id, lev_name)
lev=H5D_READ(lev_id)

; Read attribute.
attr_id=H5A_OPEN_NAME(lev_id, 'Units')
units_lev=H5A_READ(attr_id)
H5A_CLOSE,attr_id

; Close dataset.
H5D_CLOSE, lev_id

; Read time.
time_name='/SoundingHeader/sounding_time_tai93'
time_id=H5D_OPEN(file_id, time_name)
time=H5D_READ(time_id)

; Close dataset.
H5D_CLOSE, time_id

; Close file.
H5F_CLOSE, file_id

; Create an elapsed time variable.
time_e = time - time(0)

; Generate the plot.
levels = 254
DEVICE, DECOMPOSED=0
LoadCT, 33, NCOLORS=levels, BOTTOM=1

; Plot 3 graphs. Make sure that your screen's size is bigger than 800 x 1200.
WINDOW, Title=file_name, XSIZE=800, YSIZE=400*3

; Plot time vs CO2.
; /YNOZERO is required to scale the graph properly.
PLOT, time_e, data, POSITION=[0.1, 0.67, 0.93, 0.92], XTITLE='Elapsed Time (sec)', /YNOZERO
XYOuts, 0.1, 0.93, /NORMAL, 'FIELD:CO2 column averaged dry air mole fraction', $
        CHARSIZE=1.25,  ALIGNMENT=0.0
XYOuts, 0.93, 0.93, /NORMAL, 'UNIT:' + units, $ 
        CHARSIZE=1.25,  ALIGNMENT=1.0
XYOuts, 0.5, 0.96, /NORMAL, file_name, $
        CHARSIZE=1.75,  ALIGNMENT=0.5

; Plot time vs Altitude.
; Without the /NOERASE option, the previous plot will be erased.
PLOT, time_e, lev, POSITION=[0.1, 0.35,  0.93, 0.6], XTITLE='ElapsedTime (sec)', /NOERASE
XYOuts, 0.1, 0.61, /NORMAL, 'FIELD:Altitude', $
        CHARSIZE=1.25,  ALIGNMENT=0.0
XYOuts, 0.93, 0.61, /NORMAL, 'UNIT:' + units_lev, $ 
        CHARSIZE=1.25,  ALIGNMENT=1.0

; Plot the trajectory on map.
MAP_SET, /GRID, /CONTINENTS, XMARGIN=5, YMARGIN=5, $
         POSITION=[0.1, 0.05, 0.93, 0.28], /NOERASE
MAP_GRID, /BOX_AXES, COLOR=255
MAP_CONTINENTS, COLOR=255
OPLOT, lon, lat
PLOTS, lon(0), lat(0), COLOR=250, PSYM=1, SYMSIZE=2, THICK=2
XYOUTS, 0.5, 0.3, /NORMAL, 'Trajectory of Satellite Path (+:starting point)', $
        CHARSIZE=1.75, COLOR=255, ALIGNMENT=0.5

; Write JPEG image file.
im = TVRD(TRUE=3)
jpg = file_name + '.idl.jpg'   
WRITE_JPEG, jpg, im, QUALITY=100, TRUE=3

; References
;
; [1] ftp://aurapar1u.ecs.nasa.gov/ftp/data/s4pa/GOSAT_TANSO_Level2/ACOS_L2S.002/doc/README.ACOS_L2S_v2.8.pdf
; [2] http://www.dfanning.com/documents/programs.html
; 
