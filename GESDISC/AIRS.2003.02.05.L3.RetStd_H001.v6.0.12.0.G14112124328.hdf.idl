;
; This example code illustrates how to access and visualize GESDISC
; AIRS Grid file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
; If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
;
; Usage:
;
;  %idl AIRS.2003.02.05.L3.RetStd_H001.v6.0.12.0.G14112124328.hdf.idl
;
; Tested under: IDL 8.2.3
; Last updated: 2016-11-22

; Open file.
file_name='AIRS.2003.02.05.L3.RetStd_H001.v6.0.12.0.G14112124328.hdf'
file_id = EOS_GD_OPEN(file_name)

; Define grid name.
grid_name='ascending_MW_only'
datafield_name='Temperature_MW_A'

; Retrieve data.
grid_id = EOS_GD_ATTACH(file_id, grid_name)
status = EOS_GD_READFIELD(grid_id, datafield_name, data)

; Retrive fillvalue.
status=EOS_GD_GETFILLVALUE(grid_id, datafield_name, fillvalue)
status = EOS_GD_DETACH(grid_id)

; Retrieve lat/lon.
grid_name='location'
lon_name='Longitude'
grid_id = EOS_GD_ATTACH(file_id, grid_name)
status = EOS_GD_READFIELD(grid_id, lon_name, lon)
status = EOS_GD_DETACH(grid_id)

grid_name='location'
lat_name='Latitude'
grid_id = EOS_GD_ATTACH(file_id, grid_name)
status = EOS_GD_READFIELD(grid_id, lat_name, lat)
status = EOS_GD_DETACH(grid_id)
status = EOS_GD_CLOSE(file_id)

; Convert the 3D data to 2D data at TempPrsLvls=0.
data2D = data[*,*,0]
data2D = Reform(data2D)

; Convert data type.
dataf=float(data2D)
fillvaluef=float(fillvalue(0))

; Process fill values, convert data that are equal to fillvalue to NaN.
idx=WHERE(dataf eq fillvaluef, cnt)
IF cnt GT 0 THEN dataf[idx] = !Values.F_NAN


; Get max and min value of data for color bar.
datamin = MIN(dataf, /NAN)
datamax = MAX(dataf, /NAN)

m = MAP('Geographic', TITLE=file_name)
ct = COLORTABLE(72, /reverse)

t1 = TEXT(0.35, 0.01, datafield_name+' at TempPrsLvls=0')
c1 = CONTOUR(dataf, lon, lat, /FILL, OVERPLOT=m, $
             RGB_TABLE=ct, $
             GRID_UNITS='degrees', POSITION=[0.1, 0.1, 0.83, 0.9])
mc = MAPCONTINENTS()

; See the specification [1]. The dataset doesn't have unit attribute.
unit = 'K'
cb = COLORBAR(TARGET=c1, /BORDER, ORIENTATION=1, TEXTPOS=1, POSITION=[0.85,0.2,0.87,0.8], TITLE=unit)
png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT


; Reference
;
; [1] http://disc.sci.gsfc.nasa.gov/AIRS/documentation/v6_docs/v6releasedocs-1/V6_L3_User_Guide.pdf

