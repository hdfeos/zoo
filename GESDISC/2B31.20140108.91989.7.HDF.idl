;
;  This example code illustrates how to access and visualize GES DISC TRMM
;  2B31 HDF4 swath file in IDL. 
;
;  If you have any questions, suggestions, comments on this example, please 
; use the HDF-EOS Forum (http://hdfeos.org/forums). 
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS data 
; product that is not listed in the HDF-EOS Comprehensive Examples page 
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org 
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
;
; Usage:
;                               
; %idl 2B31.20140108.91989.7.HDF.idl
;
; Tested under: IDL 8.6.0
; Last updated: 2017-12-07
;
; Open file.
file_name='2B31.20140108.91989.7.HDF'
newFileID=HDF_SD_START(file_name, /READ)

; Define datafield name.
datafield_name="rrSurf"
index=HDF_SD_NAMETOINDEX(newFileID,datafield_name)

; Read data.
thisSdsID=HDF_SD_SELECT(newFileID, index)
HDF_SD_GETDATA, thisSdsID, data

; Read attribute.
units_index=HDF_SD_ATTRFIND(thisSdsID, 'units')
HDF_SD_ATTRINFO, thisSdsID, units_index, DATA=units

; Retrieve lat/lon.
geofield_name="Latitude"
index=HDF_SD_NAMETOINDEX(newFileID,geofield_name)
thisSdsID=HDF_SD_SELECT(newFileID, index)
HDF_SD_GETDATA, thisSdsID, lat

geofield_name="Longitude"
index=HDF_SD_NAMETOINDEX(newFileID,geofield_name)
thisSdsID=HDF_SD_SELECT(newFileID, index)
HDF_SD_GETDATA, thisSdsID, lon

; Close file.
HDF_SD_END, newFileID

; Convert data type.
fillvalue=min(data)

; Handle fill value.
idx=where(data eq fillvalue, cnt)
if cnt gt 0 then data[idx] = !Values.F_NAN

; Plot data.
m = MAP('Geographic', TITLE=file_name, FONT_SIZE=9, /BUFFER)
ct = COLORTABLE(72, /reverse)
t1 = TEXT(0.05, 0.2, FONT_SIZE=8, datafield_name)
c1 = CONTOUR(data, lon, lat, /FILL, OVERPLOT=m, $
             RGB_TABLE=ct, $
             GRID_UNITS='degrees', POSITION=[0.1, 0.1, 0.83, 0.9])
mc = MAPCONTINENTS()
cb = COLORBAR(TARGET=c1, /BORDER, ORIENTATION=1, TEXTPOS=1, POSITION=[0.85,0.2,0.87,0.8], TITLE=units)
png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT
