; This example code illustrates how to access and visualize GESDISC
; TRMM Swath file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
; If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; Open file
  file_name='3B42.100331.21.6A.HDF'
  newFileID=HDF_SD_START(file_name, /READ)

; Define datafield name
  datafield_name="precipitation"
  index=HDF_SD_NAMETOINDEX(newFileID,datafield_name)

; retrieve data
  thisSdsID=HDF_SD_SELECT(newFileID, index)
  HDF_SD_GETDATA, thisSdsID, data

; Convert the M-D data to 2D data at scan=0
  data2D=data[*,*,0]

; transpose data
  data2D=transpose(data2D)

; Close file
  HDF_SD_END, newFileID

; lat and lon were acquried based on the information from this website: ; http://disc.sci.gsfc.nasa.gov/additional/faq/precipitation_faq.shtml#lat_lon

  lat= FINDGEN(400)*0.25-49.875
  lon= FINDGEN(1440)*0.25-179.875

; read fillvalue from data
  fillvalue=0.00

; get max and min value of data
  idx=where(data2D ne fillvalue, cnt)
  if cnt gt 0 then datamin=MIN(data2D[idx])
  if cnt gt 0 then datamax=MAX(data2D[idx])

; Process fill values, convert data that are equal to fillvalue to NaN
  idx=where(data2D eq fillvalue, cnt)
; if cnt gt 0 then data2D[idx]=!Values.F_NAN

; The following coding is prepared for colorbar. If you require colorbar in your plot, you could download Dr. 
; Fan's Coyote Library from (http://www.dfanning.com/documents/programs.html). Make a directory named coyote 
; somewhere on your machine, and extract the Coyote files into it. If color bar is not not necessary for your
; plot, you can ignore this step and add comment character ';' ahead of coding. 
; Add the coyote directory you create on your machine to your IDL path. 
; !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH
  !PATH=EXPAND_PATH('+coyote/')+':'+!PATH

; Prepare field name title using long name attribute.
  field = datafield_name
  units = 'mm/hr'

; Generate a plot.
  levels=254
  device,decomposed=0
  LoadCT,33, Ncolors=levels, Bottom=1
  WINDOW, Title = 'FIELD:' + field + ' at scan=0' + '   ' + 'UNIT:' + units, XSIZE=800, YSIZE=500
  MAP_SET, /GRID, /CONTINENTS, XMARGIN=5, YMARGIN=5, $
    POSITION=[0.05, 0.08, 0.82, 0.82]
  CONTOUR, data2D, lon, lat, /OVERPLOT, /CELL_FILL, C_Colors=Indgen(levels)+3, Background=1, NLEVELS=levels, Color=Black
  MAP_GRID, /BOX_AXES, COLOR=255
  MAP_CONTINENTS, COLOR=255

; Draw file name, field name, and unit.
  XYOUTS, 0.05, 0.88, /NORMAL, 'FIELD:' + field + ' at scan=0', $
    CHARSIZE=1.25,  ALIGNMENT=0.0
  XYOUTS, 0.94, 0.88, /NORMAL, 'UNIT:' + units, $ 
    CHARSIZE=1.25,  ALIGNMENT=1.0
  XYOUTS, 0.43, 0.92, /NORMAL, file_name, $
    CHARSIZE=1.75,  ALIGNMENT=0.5

; The following code is prepared for colorbar. 
;
; If you want to have a color bar in your plot, please download 
; "Dr. Fanning's Coyote Library" from [1]. Make a directory named
; coyote somewhere on your machine, and extract the Coyote library files into
; it. 
;
; If color bar is not not necessary for your plot, you can ignore this
; step and add comment character ';' at the beginning of the rest of codes.
;
; Add the coyote directory that you created on your machine to your IDL
; path like below: 
;
; !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH

; We assume that the coyote library is installed under the current working
; directory that this code exists.
  !PATH=EXPAND_PATH('+coyote/')+':'+!PATH

; The following code assumes that you've already download and installed 
; "Dr. Fanning's Coyote Library" and add the coyote directory above. 
;
; If you don't need a color bar in your plot, you can ignore this step
; by adding comment character ';' at the beginning of the code.
  COLORBAR, RANGE=[datamin, datamax], NCOLORS=levels, /VERTICAL, $
   POSITION=[0.9,0.08,0.94,0.8]

; Write JPEG image file.
  im = TVRD(TRUE=3)
  jpg = '3B42.100331.21.6A_precipitation_level0.idl.JPG' 
  WRITE_JPEG, jpg, im, QUALITY=100, TRUE=3

; Reference
;
; [1] http://www.dfanning.com/documents/programs.html