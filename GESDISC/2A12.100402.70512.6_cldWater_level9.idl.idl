; This example code illustrates how to access and visualize GESDISC
; TRMM Swath file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
; If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; Open file.
file_name='2A12.100402.70512.6.HDF'
newFileID=HDF_SD_START(file_name, /READ)

; Define datafield name.
datafield_name="cldWater"
index=HDF_SD_NAMETOINDEX(newFileID,datafield_name)

; Retrieve data.
thisSdsID=HDF_SD_SELECT(newFileID, index)
HDF_SD_GETDATA, thisSdsID, data

; Convert the 3D data to 2D data at layer=9.
data2D=data[9,*,*]

; Retrieve scale factor and offset.
scalefactor_index=HDF_SD_ATTRFIND(thisSdsID, 'scale_factor')
HDF_SD_ATTRINFO, thisSdsID, scalefactor_index, DATA=scale_factor

offset_index=HDF_SD_ATTRFIND(thisSdsID, 'add_offset')
HDF_SD_ATTRINFO, thisSdsID, offset_index, DATA=offset

; Retrieve lat/lon.
geofield_name="geolocation"
index=HDF_SD_NAMETOINDEX(newFileID,geofield_name)

thisSdsID=HDF_SD_SELECT(newFileID, index)
HDF_SD_GETDATA, thisSdsID, geolocation

lon=geolocation(1,*,*)
lat=geolocation(0,*,*)

; Close file.
HDF_SD_END, newFileID

; Read fillvalue from data.
fillvalue=-9999

; Convert data type.
dataf=float(data2D)

; Process fill values.
idx=where(data2D eq fillvalue, cnt) 
if cnt gt 0 then dataf[idx] = !Values.F_NAN

; Apply scale and offset according to [1].
dataf=offset(0) + dataf / (scale_factor(0))

; Get max and min value of data for plot.
datamin = MIN(dataf, /NAN)
datamax = MAX(dataf, /NAN)

; The unit is g/m(-3). See [1].
units = 'g/m!E-3' 

; The following code is prepared for colorbar. 
;
;   If you want to have a color bar in your plot, please download 
; "Dr. Fanning's Coyote Library" from [2]. Make a directory named
; coyote somewhere on your machine, and extract the Coyote library files into
; it. 
;
;   If color bar is not not necessary for your plot, you can ignore this
; step and add comment character ';' at the beginning of the rest of codes.
;
;   Add the coyote directory that you created on your machine to your IDL
;  path like below: 
;
; !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH

;   We assume that the coyote library is installed under the current working
; directory that this code exists.
!PATH=EXPAND_PATH('+coyote/')+':'+!PATH

; Prepare field name title using long name attribute.
field = datafield_name

; Generate a plot.
levels=254
DEVICE, DECOMPOSED=0
LoadCT,33, NCOLORS=levels, BOTTOM=1
WINDOW, Title= 'FIELD:' + field + ' at layer=9', XSIZE=900, YSIZE=680
MAP_SET, /GRID,/CONTINENTS, XMARGIN=5, YMARGIN=5, $
  POSITION=[0.05, 0.05, 0.82, 0.82]

; Unlike other example IDL plots, you can not use 
;
; CONTOUR, dataf, lon, lat, /OVERPLOT, /CELL_FILL, C_COLORS=Indgen(levels)+3, $
;    NLEVELS=levels
; 
; when the minimum value (datamin) is 0.0.
;
; CONTOUR will plot 0.0 values with black color as if they are fill
; values, which means you can't see data points with 0.0 values.
; 
; We believe this is a bug in IDL. Thus, we'll use explicit levels.
CONTOUR, dataf, lon, lat, /CELL_FILL, /OVERPLOT, $
  LEVELS=FINDGEN(levels)*((datamax - datamin)/FLOAT(levels)), $
  C_COLORS=Indgen(levels)+1

MAP_GRID, /BOX_AXES, COLOR=WHITE
MAP_CONTINENTS

; Draw file name, field name, and unit.
XYOUTS, 0.05, 0.88, /NORMAL, 'FIELD:' + field + ' at layer=9', $
  CHARSIZE=1.75,  ALIGNMENT=0.0
XYOUTS, 0.94, 0.88, /NORMAL, 'UNIT:' + units, $ 
  CHARSIZE=1.75,  ALIGNMENT=1.0
XYOUTS, 0.43, 0.92, /NORMAL, file_name, $
  CHARSIZE=1.80,  ALIGNMENT=0.5

; The following code is prepared for colorbar. 
;
; If you want to have a color bar in your plot, please download 
; "Dr. Fanning's Coyote Library" from [1]. Make a directory named
; coyote somewhere on your machine, and extract the Coyote library files into
; it. 
;
; If color bar is not not necessary for your plot, you can ignore this
; step and add comment character ';' at the beginning of the rest of codes.
;
; Add the coyote directory that you created on your machine to your IDL
; path like below: 
;
; !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH

; We assume that the coyote library is installed under the current working
; directory that this code exists.
!PATH=EXPAND_PATH('+coyote/')+':'+!PATH

; The following code assumes that you've already download and installed 
; "Dr. Fanning's Coyote Library" and add the coyote directory above. 
;
; If you don't need a color bar in your plot, you can ignore this step
; by adding comment character ';' at the beginning of the code.

;  BOTTOM=1 is necessary to remove background color (black) from color
;  bar.
COLORBAR, RANGE=[datamin, datamax], NCOLORS=levels, BOTTOM=1, $
   POSITION=[0.9,0.05,0.94,0.8], FORMAT='(F5.3)', /VERTICAL

; Write a JPEG image file.
im = TVRD(TRUE=3)
jpg = '2A12.100402.70512.6_cldWater_level9.idl.JPG' 
WRITE_JPEG, jpg, im, QUALITY=100, TRUE=3

; Reference
;
; [2] http://www.dfanning.com/documents/programs.html
; [1] http://disc.sci.gsfc.nasa.gov/precipitation/documentation/TRMM_README/TRMM_2A12_readme.shtml

