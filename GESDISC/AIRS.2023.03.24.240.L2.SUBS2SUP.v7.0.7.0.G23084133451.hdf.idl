;
; This example code illustrates how to access and visualize GESDISC
; AIRS Swath v7 file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
; If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; Usage:
;
;  $idl AIRS.2023.03.24.240.L2.SUBS2SUP.v7.0.7.0.G23084133451.hdf.idl
;
; Tested under: IDL 8.8.3
; Last updated: 2023-05-03

; Open file.
file_name='AIRS.2023.03.24.240.L2.SUBS2SUP.v7.0.7.0.G23084133451.hdf'

; Define Swath name.
swath_name='L2_Support_atmospheric&surface_product'
file_id=EOS_SW_OPEN(file_name)
swath_id=EOS_SW_ATTACH(file_id, swath_name)

; Retrieve Swath data.
datafield_name='olr'
status=EOS_SW_READFIELD(swath_id, datafield_name, data2D)

; Convert data type
dataf=FLOAT(data2D)

; Retrieve lat/lon
status=EOS_SW_READFIELD(swath_id,'Longitude',lon)
status=EOS_SW_READFIELD(swath_id,'Latitude',lat)

; Print whether it's asceding or descending.
status=EOS_SW_READATTR(swath_id, 'node_type', attr)
PRINT, attr

status=EOS_SW_DETACH(swath_id)
status=EOS_SW_CLOSE(file_id)

; Get vertex value for lat and lon
latmin=MIN(lat)
latmax=MAX(lat)
lonmin=MIN(lon)
lonmax=MAX(lon)

; Read fillvalue using HDFView.
fillvalue=-9999.00
fillvaluef=FLOAT(fillvalue(0))

; Process fill values, convert data that are equal to fillvalue to NaN
idx=WHERE(dataf EQ fillvaluef, cnt)
IF cnt GT 0 THEN dataf[idx] = !Values.F_NAN


; Get max and min value of data for color bar.
datamin = MIN(dataf, /NAN)
datamax = MAX(dataf, /NAN)

m = MAP('Geographic', TITLE=file_name, FONT_SIZE=9, /BUFFER)
ct = COLORTABLE(72, /reverse)
; See [1].
t1 = TEXT(0.35, 0.2, 'Outgoing Longwave Radiation')

datamin=MIN(dataf)
datamax=MAX(dataf)

c1 = SCATTERPLOT(lon(*), lat(*), OVERPLOT=m, $
                 MAGNITUDE=dataf(*), $
                 RGB_TABLE=ct, $
                 POSITION=[0.1, 0.1, 0.83, 0.9],$
                 /SYM_FILLED, SYMBOL='o', SYM_SIZE=0.1)
mc = MAPCONTINENTS()

; See [1]. The dataset doesn't have unit attribute.
unit = 'W/m^2'
cb = COLORBAR(RGB_TABLE=ct, RANGE=[datamin, datamax], /BORDER, ORIENTATION=1, TEXTPOS=1, POSITION=[0.85,0.2,0.87,0.8], TITLE=unit)
png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT


; Reference
;
; [1] https://airs.jpl.nasa.gov/data/products/v7-L2-L3/

