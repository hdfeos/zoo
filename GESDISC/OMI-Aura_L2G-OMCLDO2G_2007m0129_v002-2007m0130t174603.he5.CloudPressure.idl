; This example code illustrates how to access and visualize GESDISC
; OMI GRID file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
; If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; Open file.
  file_name='OMI-Aura_L2G-OMCLDO2G_2007m0129_v002-2007m0130t174603.he5'
  file_id=H5F_OPEN(file_name)

; Read data field.
  datafield_name= '/HDFEOS/GRIDS/CloudFractionAndPressure/Data Fields/CloudPressure'
  data_id=H5D_OPEN(file_id, datafield_name)

  dataspace_id=H5D_GET_SPACE(data_id)
  Dims=H5S_GET_SIMPLE_EXTENT_DIMS(dataspace_id)
  Dims=float(Dims)

  data3d=H5D_READ(data_id)

; Retrieve units, missing value, scale_factor and offset.
  units_id=H5A_OPEN_NAME(data_id, 'Units')
  units=H5A_READ(units_id)
  H5A_CLOSE, units_id

  slope_id=H5A_OPEN_NAME(data_id, 'ScaleFactor')
  slope=H5A_READ(slope_id)
  H5A_CLOSE, slope_id

  intercept_id=H5A_OPEN_NAME(data_id, 'Offset')
  intercept=H5A_READ(intercept_id)
  H5A_CLOSE, intercept_id

  missingvalue_id=H5A_OPEN_NAME(data_id,'MissingValue')
  missingvalue=H5A_READ(missingvalue_id)
  H5A_CLOSE, missingvalue_id
  H5D_CLOSE, data_id

; Read latitude.
  lat_name='/HDFEOS/GRIDS/CloudFractionAndPressure/Data Fields/Latitude'
  lat_id=H5D_OPEN(file_id, lat_name)
  lat3d=H5D_READ(lat_id)

; Retrieve the missing value attribute of latitude.
  missingvalue_id=H5A_OPEN_NAME(lat_id, 'MissingValue')
  missingvalue_lat=H5A_READ(missingvalue_id)
  H5A_CLOSE, missingvalue_id
  H5D_CLOSE, lat_id

; Read longitude.
  lon_name='/HDFEOS/GRIDS/CloudFractionAndPressure/Data Fields/Longitude'
  lon_id=H5D_OPEN(file_id, lon_name)
  lon3d=H5D_READ(lon_id)

; Retrieve the missing value attribute of longitude.
  missingvalue_id=H5A_OPEN_NAME(lon_id, 'MissingValue')
  missingvalue_lon=H5A_READ(missingvalue_id)
  H5A_CLOSE, missingvalue_id
  H5D_CLOSE, lon_id
  H5F_CLOSE, file_id
  
; Copy the data.
  dataf3d=float(data3d)
  latf3d=float(lat3d)
  lonf3d=float(lon3d)


; Process missing value.
  idx=where(data3D eq missingvalue(0), cnt)
  if cnt gt 0 then dataf3d[idx] = !Values.F_NAN

  idx=where(lat3D eq missingvalue_lat(0), cnt)
  if cnt gt 0 then latf3d[idx] = !Values.F_NAN

  idx=where(lon3D eq missingvalue_lon(0), cnt)
  if cnt gt 0 then lonf3d[idx] = !Values.F_NAN

; Apply scale and offset on data.
  dataf3d=(slope(0))*(dataf3d-intercept(0))

; Subset at nCandidate = 0.
  nCandidate = 0
  dataf = dataf3d(*,*,nCandidate);
  lat = latf3d(*,*,nCandidate)
  lon = lonf3d(*,*,nCandidate)

; Get max and min value of data for color bar.
  datamin = MIN(dataf, /NAN)
  datamax = MAX(dataf, /NAN)

; Prepare field name title using long name attribute.
  field = 'ColudPressure at nCandidate=' + STRING(nCandidate, FORMAT='(I1)')

; Generate the plot.
  levels = 254
  DEVICE, DECOMPOSED=0
  LOADCT, 33, NCOLORS=levels, BOTTOM=1
  WINDOW, TITLE = 'FIELD:' + field  + ' UNIT:' + units, $
    XSIZE=800, YSIZE=500
  MAP_SET, /GRID, /CONTINENTS,XMARGIN=5, YMARGIN=5, $
    POSITION=[0.05, 0.06, 0.82, 0.80]
  CONTOUR, dataf, lon, lat, /OVERPLOT, /CELL_FILL, C_Colors=Indgen(levels)+3, $
    BACKGROUND=1, NLEVELS=levels, COLOR=Black
  MAP_GRID, /BOX_AXES, COLOR=255
  MAP_CONTINENTS, COLOR=255

; Draw file name, field name, and unit.
  XYOUTS, 0.05, 0.86, /NORMAL, 'FIELD:' + field, $
    CHARSIZE=1.25,  ALIGNMENT=0.0
  XYOUTS, 0.94, 0.86, /NORMAL, 'UNIT:' + units, $ 
    CHARSIZE=1.25,  ALIGNMENT=1.0
  XYOUTS, 0.49, 0.92, /NORMAL, file_name, $
    CHARSIZE=1.75,  ALIGNMENT=0.5

; The following code is prepared for colorbar. 
;
; If you want to have a color bar in your plot, please download 
; "Dr. Fanning's Coyote Library" from [1]. Make a directory named
; coyote somewhere on your machine, and extract the Coyote library files into
; it. 
;
; If color bar is not not necessary for your plot, you can ignore this
; step and add comment character ';' at the beginning of the rest of codes.
;
; Add the coyote directory that you created on your machine to your IDL
; path like below: 
;
; !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH

; We assume that the coyote library is installed under the current working
; directory that this code exists.
  !PATH=EXPAND_PATH('+coyote/')+':'+!PATH

; The following code assumes that you've already download and installed 
; "Dr. Fanning's Coyote Library" and add the coyote directory above. 
;
; If you don't need a color bar in your plot, you can ignore this step
; by adding comment character ';' at the beginning of the code.
  COLORBAR, RANGE=[datamin, datamax], NCOLORS=levels, /VERTICAL, $
   POSITION=[0.9,0.08,0.94,0.8]

; Write JPEG image file.
  field_name = 'CloudPressure'
  im = TVRD(TRUE=3)
  jpg = file_name + '.' + field_name + '.idl.jpg'
  WRITE_JPEG, jpg, im, QUALITY=100, TRUE=3

; Reference
;
; [1] http://www.dfanning.com/documents/programs.html
