;
; This example code illustrates how to access and visualize GES DISC
; TRMM 3B42 HDF4 Grid file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
; If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
; Usage:
;                               
; %idl 3B42.20131130.21.7.HDF.idl
;
; Tested under: IDL 8.6.0
; Last updated: 2017-12-08
;
; Open file.
  file_name='3B42.20131130.21.7.HDF'
  newFileID=HDF_SD_START(file_name, /READ)

; Define dataset to read.
  datafield_name="precipitation"
  index=HDF_SD_NAMETOINDEX(newFileID,datafield_name)

; Read dataset.
  thisSdsID=HDF_SD_SELECT(newFileID, index)
  HDF_SD_GETDATA, thisSdsID, data

; Read attribute.
units_index=HDF_SD_ATTRFIND(thisSdsID, 'units')
HDF_SD_ATTRINFO, thisSdsID, units_index, DATA=units

; Transpose data.
  data=transpose(data)

; Close file.
  HDF_SD_END, newFileID

; Generate lat/lon data based on user guide [1].
  lat= FINDGEN(400)*0.25-49.875
  lon= FINDGEN(1440)*0.25-179.875

; Set fill value from [1].
  fillvalue=0.00

; Handle fill value.
idx=where(data eq fillvalue, cnt)
if cnt gt 0 then data[idx] = !Values.F_NAN

; Plot data.
m = MAP('Geographic', TITLE=file_name, FONT_SIZE=9, /BUFFER)
ct = COLORTABLE(72, /reverse)
t1 = TEXT(0.05, 0.2, FONT_SIZE=8, datafield_name)
c1 = CONTOUR(data, lon, lat, /FILL, OVERPLOT=m, $
             RGB_TABLE=ct, $
             GRID_UNITS='degrees', POSITION=[0.1, 0.1, 0.83, 0.9])
mc = MAPCONTINENTS()
cb = COLORBAR(TARGET=c1, /BORDER, ORIENTATION=1, TEXTPOS=1, POSITION=[0.85,0.2,0.87,0.8], TITLE=units)
png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT
; References
; [1] https://pmm.nasa.gov/sites/default/files/document_files/3B42_3B43_doc_V7.pdf
