;
; This example code illustrates how to access and visualize GESDISC
; AIRS Swath file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
; If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; Usage:
;
;  %idl AIRS.2002.08.30.227.L2.RetStd_H.v6.0.12.0.G14101125810.hdf.idl
;
; Tested under: IDL 8.2.3
; Last updated: 2016-11-22

; Open file.
file_name='AIRS.2002.08.30.227.L2.RetStd_H.v6.0.12.0.G14101125810.hdf'

; Define Swath name.
swath_name='L2_Standard_atmospheric&surface_product'
file_id=EOS_SW_OPEN(file_name)
swath_id=EOS_SW_ATTACH(file_id, swath_name)

; Retrieve Swath data.
datafield_name='topog'
status=EOS_SW_READFIELD(swath_id,datafield_name,data2D)

; Convert data type
dataf=FLOAT(data2D)

; retrieve lat/lon
status=EOS_SW_READFIELD(swath_id,'Longitude',lon)
status=EOS_SW_READFIELD(swath_id,'Latitude',lat)
status=EOS_SW_DETACH(swath_id)
status=EOS_SW_CLOSE(file_id)

; Get vertex value for lat and lon
latmin=MIN(lat)
latmax=MAX(lat)
lonmin=MIN(lon)
lonmax=MAX(lon)

; Read fillvalue using HDFView.
fillvalue=-9999.00
fillvaluef=FLOAT(fillvalue(0))

; Process fill values, convert data that are equal to fillvalue to NaN
idx=WHERE(dataf EQ fillvaluef, cnt)
IF cnt GT 0 THEN dataf[idx] = !Values.F_NAN


; Get max and min value of data for color bar.
datamin = MIN(dataf, /NAN)
datamax = MAX(dataf, /NAN)

m = MAP('Polar Stereographic', $
  LIMIT=[latmin, lonmin, latmax, lonmax], $
  CENTER_LATITUDE=90.0, $
  TITLE=file_name)
ct = COLORTABLE(72, /reverse)

; Do not use /FILL to handle fill values correctly on polar
; stereographic projection.
t1 = TEXT(0.35, 0.01, datafield_name)
c1 = CONTOUR(dataf, lon, lat, /FILL, OVERPLOT=m, $
             RGB_TABLE=ct, $
             GRID_UNITS='degrees')
mc = MAPCONTINENTS()

; See the specification [1]. The dataset doesn't have unit attribute.
unit = 'm'
cb = COLORBAR(TARGET=c1, /BORDER, ORIENTATION=1, TEXTPOS=1,  Position=[0.85,0.1,0.87,0.8], TITLE=unit)
png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT


; Reference
;
; [1] http://disc.sci.gsfc.nasa.gov/AIRS/documentation/v6_docs/v6releasedocs-1/V6_L2_Product_User_Guide.pdf

