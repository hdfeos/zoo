;
; This example code illustrates how to access and visualize GES DISC
; TRMM 3A26 v7 HDF4 Grid file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
; If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
; Usage:
;                               
; %idl 3A26.20140101.7.HDF.idl
;
; Tested under: IDL 8.6.0
; Last updated: 2017-12-18
;
; Open file.
  file_name='3A26.20140101.7.HDF'
  newFileID=HDF_SD_START(file_name, /READ)

; Define dataset to read.
  datafield_name="rainMeanTH"
  index=HDF_SD_NAMETOINDEX(newFileID,datafield_name)

; Read dataset.
  thisSdsID=HDF_SD_SELECT(newFileID, index)
  HDF_SD_GETDATA, thisSdsID, data

; Read attribute.
units_index=HDF_SD_ATTRFIND(thisSdsID, 'units')
HDF_SD_ATTRINFO, thisSdsID, units_index, DATA=units

; Transpose data.
  data=transpose(data(*,*,0))

; Close file.
  HDF_SD_END, newFileID

; Generate lat/lon data based on user guide [1].
  lat= FINDGEN(16)*5.0-49.875
  lon= FINDGEN(72)*5.0-179.875

; Handle fill value.
idx=where(data < 0.0, cnt)
if cnt gt 0 then data[idx] = !Values.F_NAN

; Plot data.
m = MAP('Geographic', TITLE=file_name, FONT_SIZE=9, /BUFFER)
ct = COLORTABLE(72, /reverse)
t1 = TEXT(0.05, 0.2, FONT_SIZE=8, datafield_name + ' at nh3=0' )
c1 = CONTOUR(data, lon, lat, /FILL, OVERPLOT=m, $
             RGB_TABLE=ct, $
             GRID_UNITS='degrees', POSITION=[0.1, 0.1, 0.83, 0.9])
mc = MAPCONTINENTS()
cb = COLORBAR(TARGET=c1, /BORDER, ORIENTATION=1, TEXTPOS=1, POSITION=[0.85,0.2,0.87,0.8], TITLE=units)
png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT
; References
; [1] https://pps.gsfc.nasa.gov/Documents/filespec.TRMM.V7.pdf
