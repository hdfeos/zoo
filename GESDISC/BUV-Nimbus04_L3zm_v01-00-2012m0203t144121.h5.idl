;
;  This example code illustrates how to access and visualize GESDISC MEaSUREs
; Ozone Zonal Average HDF5 file in IDL.
;
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
; If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; Tested under: IDL 8.0
; Last updated: 2012-10-17

; Open file.
file_name='BUV-Nimbus04_L3zm_v01-00-2012m0203t144121.h5'
file_id=H5F_OPEN(file_name)

; Read data.
datafield_name='/Data_Fields/ProfileOzone'
data_id=H5D_OPEN(file_id,datafield_name)
data=H5D_READ(data_id)

; Get title.
title_id=H5A_OPEN_NAME(data_id, 'long_name')
title=H5A_READ(title_id)
H5A_CLOSE, title_id

; Get units.
units_id=H5A_OPEN_NAME(data_id, 'units')
units=H5A_READ(units_id)
H5A_CLOSE, units_id

; Get fill value.
fillvalue_id=H5A_OPEN_NAME(data_id,'_FillValue')
fillvalue=H5A_READ(fillvalue_id)
H5A_CLOSE, fillvalue_id

; Close dataset.
H5D_CLOSE, data_id

; Read latitude.
latitude_name='/Data_Fields/Latitude'
latitude_id=H5D_OPEN(file_id, latitude_name)
lat=H5D_READ(latitude_id)

; Get title.
title_id=H5A_OPEN_NAME(latitude_id, 'long_name')
title_lat=H5A_READ(title_id)
H5A_CLOSE, title_id

; Get units.
units_id=H5A_OPEN_NAME(latitude_id, 'units')
units_lat=H5A_READ(units_id)
H5A_CLOSE, units_id

; Close dataset.
H5D_CLOSE, latitude_id

; Read Pressure.
pressure_name='/Data_Fields/ProfilePressureLevels'
pressure_id=H5D_OPEN(file_id, pressure_name)
pressure=H5D_READ(pressure_id)

; Get title.
title_id=H5A_OPEN_NAME(pressure_id, 'long_name')
title_pressure=H5A_READ(title_id)
H5A_CLOSE, title_id

; Get units.
units_id=H5A_OPEN_NAME(pressure_id, 'units')
units_pressure=H5A_READ(units_id)
H5A_CLOSE, units_id

; Close dataset.
H5D_CLOSE, pressure_id

; Read Date.
date_name='/Data_Fields/Date'
date_id=H5D_OPEN(file_id, date_name)
date=H5D_READ(date_id)
H5D_CLOSE, date_id

; Close file.
H5F_CLOSE, file_id

; Convert type to double.
dataf = FLOAT(data)

; Process fill value.
idx=WHERE(data EQ fillvalue(0), cnt)
IF cnt GT 0 THEN dataf[idx] = !Values.F_NAN


; Apply log scale on Pressure to get a better image.
pressure = ALOG10(pressure)

; Subset at index 0.
index_s = 0

; Transpose data to plot pressure (Y-axis) vs latitude (X-axis).
dataf = TRANSPOSE(dataf(*,*,index_s))

; This product's time is not in TAI 1993 format. 
; It uses a 4+2 digit number that indicates year and month.
tstring = STRING(date(index_s), FORMAT='(I6)')

;   The following code is prepared for colorbar. 
;
;   If you require colorbar in your plot, you could download 
; "Dr. Fanning's Coyote Library" from [1]. Make a directory named
; coyote somewhere on your machine, and extract the Coyote files into
; it. 
;
;   If color bar is not not necessary for your plot, you can ignore this
; step and add comment character ';' ahead of coding. 
;
;    Add the coyote directory you create on your machine to your IDL
;  path like below: 
;
; !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH

; Let's assume that you've installed coyote library under the current working
; directory that this code exists.
!PATH=Expand_Path('+coyote/')+':'+!PATH

; Generate the plot.
LEVELS = 200
DEVICE, DECOMPOSED=0
LOADCT, 33, NCOLORS=levels, BOTTOM=3
WINDOW,Title=file_name, XSIZE=800
CONTOUR, dataf,  lat, pressure, /CELL_FILL, $
         C_COLORS=Indgen(levels)+3, $
         BACKGROUND=1, NLEVELS=levels, COLOR=Black, $
         XTITLE=title_lat +' (' + units_lat + ')', $
         YTITLE=title_pressure + ' (' + units_pressure + ') in log10', $
         POSITION=[0.1, 0.1, 0.82, 0.8]

XYOUTS, 0.1, 0.82, /NORMAL, 'FIELD:' + title, $
        CHARSIZE=1.25, COLOR=black, ALIGNMENT=0.0
XYOUTS, 0.82, 0.82, /NORMAL, 'UNIT:' + units, $ 
        CHARSIZE=1.25, COLOR=black, ALIGNMENT=1.0
XYOUTS, 0.5, 0.92, /NORMAL, file_name, $
        CHARSIZE=1.75, COLOR=black, ALIGNMENT=0.5
XYOUTS, 0.99, 0.01, /NORMAL, 'DATE:' + tstring, $
        CHARSIZE=1.25, COLOR=black, ALIGNMENT=1.0

;  The following code assumes that you've already downloaded and installed 
; "Dr. Fanning's Coyote Library" and add the coyote directory above. 
;
;  If you don't need color bar in your plot, you can ignore this step
;  by adding comment character ';' at the beginning of the code.
datamin=MIN(dataf, /NAN)
datamax=MAX(dataf, /NAN)
COLORBAR, RANGE=[datamin,datamax], NCOLORS=levels, /VERTICAL, $
          POSITION=[0.9,0.1,0.94,0.8], FORMAT='(F5.2)'

; Write JPEG image file.
im = TVRD(TRUE=3)
jpg = file_name + '.idl.jpg'   
WRITE_JPEG, jpg, im, QUALITY=100, TRUE=3
EXIT

; References
;
; [1] http://www.dfanning.com/documents/programs.html
