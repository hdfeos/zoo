;
;  This example code illustrates how to access and visualize GES DISC MEaSUREs
; SeaWiFS L2 Swath HDF5 file in NCL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
; If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
;
; Usage:
;                               
; $idl DeepBlue-SeaWiFS-1.0_L3_20100101_v004-20130604T131317Z.h5.idl
;
; Tested under: IDL 8.6.0
; Last updated: 2018-01-19

; Open file.
file_name='DeepBlue-SeaWiFS-1.0_L3_20100101_v004-20130604T131317Z.h5';
file_id=H5F_OPEN(file_name)

; Read data.
datafield_name='aerosol_optical_thickness_550_ocean'
data_id=H5D_OPEN(file_id,datafield_name)
data=H5D_READ(data_id)

; Get title.
title_id=H5A_OPEN_NAME(data_id, 'long_name')
title=H5A_READ(title_id)
H5A_Close, title_id

; Get units.
units_id=H5A_OPEN_NAME(data_id, 'units')
units=H5A_READ(units_id)
H5A_Close, units_id

; Get _FillValue.
fv_id=H5A_OPEN_NAME(data_id, '_FillValue')
fv=H5A_READ(fv_id)
H5A_Close, fv_id

; Close dataset.
H5D_Close, data_id

; Read latitude.
latitude_name='latitude'
latitude_id=H5D_OPEN(file_id, latitude_name)
lat=H5D_READ(latitude_id)

; Close dataset.
H5D_Close, latitude_id

; Read longitude.
lon_name='longitude'
lon_id=H5D_OPEN(file_id, lon_name)
lon=H5D_READ(lon_id)

; Close dataset.
H5D_Close, lon_id

; Close file.
H5F_Close, file_id

; Convert type to double.
dataf = float(data)


; Set fill value from data using HDFView.
; Please note that _FillValue is represented as 1-d array.
idx=where(data eq fv(0), cnt)
if cnt gt 0 then dataf[idx] = !Values.F_NAN

; Generate the plot.
m = MAP('Geographic', TITLE=file_name, FONT_SIZE=9, /BUFFER)
ct = COLORTABLE(72, /reverse)

t1 = TEXT(0.15, 0.2, title)
c1 = CONTOUR(dataf, lon, lat, /FILL, OVERPLOT=m, $
             RGB_TABLE=ct, $
             GRID_UNITS='degrees', POSITION=[0.1, 0.1, 0.83, 0.9])
mc = MAPCONTINENTS()
cb = COLORBAR(TARGET=c1, /BORDER, ORIENTATION=1, TEXTPOS=1, $
              POSITION=[0.85,0.2,0.87,0.8], TITLE=units)
png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT
