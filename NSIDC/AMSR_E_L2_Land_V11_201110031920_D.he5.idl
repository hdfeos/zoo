;
;  This example code illustrates how to access and visualize 
; NSIDC AMSR_E L2 HDF-EOS5 Point file in IDL.
;
;  If you have any questions, suggestions, comments on this example, please 
; use the HDF-EOS Forum (http://hdfeos.org/forums). 
; 
;  If you would like to see an  example of any other NASA HDF/HDF-EOS data 
; product that is not listed in the HDF-EOS Comprehensive Examples page 
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org 
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
;
; Usage:
;                               
; $idl AMSR_E_L2_Land_V11_201110031920_D.he5.idl
;
; Tested under: IDL 8.7.2
; Last updated: 2021-06-08

; Open file.
file_name='AMSR_E_L2_Land_V11_201110031920_D.he5'
file_id=H5F_OPEN(file_name)

; Retrieve data.
datafield_name='/HDFEOS/POINTS/AMSR-E Level 2 Land Data/Data/Combined NPD and SCA Output Fields'
data_id=H5D_OPEN(file_id, datafield_name)
data=H5D_READ(data_id)
lat_a=data.Latitude
lon_a=data.Longitude
data_a=data.SoilMoistureSCA

; Close dataset.
H5D_CLOSE, data_id

; Close file.
H5F_CLOSE, file_id

fv=-9999.0

; Filter data with missing value.
idx = where(data_a ne fv, cnt)
dataf = data_a[idx]
lat = lat_a[idx]
lon = lon_a[idx]

; Generate the plot.
m = MAP('Geographic', TITLE=file_name, FONT_SIZE=9, /BUFFER)
ct = COLORTABLE(72, /reverse)
t1 = TEXT(0.35, 0.01, FONT_SIZE=8, 'SoilMoistureSCA')
datamin=MIN(dataf)
datamax=MAX(dataf)
c1 = SCATTERPLOT(lon, lat, OVERPLOT=m, $
                 MAGNITUDE=dataf, $
                 RGB_TABLE=ct, $
                 POSITION=[0.1, 0.1, 0.83, 0.9],$
                 /SYM_FILLED, SYMBOL='o', SYM_SIZE=0.1)
mc = MAPCONTINENTS()

; We need custom colorbar because we use SCATTERPLOT().
; We cannot use TARGET=c1.
cb = COLORBAR(RGB_TABLE=ct, RANGE=[datamin, datamax], /BORDER, ORIENTATION=1, $
              TEXTPOS=1, POSITION=[0.85,0.2,0.87,0.8])

png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT

; Reference
; [1] https://disc.gsfc.nasa.gov/datasets/GPM_3HCSH_06/summary
