;
;  This example code illustrates how to access and visualize NSIDC AMSR_E
; Daily Land HDF-EOS2 Grid file in IDL. This Grid file uses
; Cylindrical Equal Area (CEA) projection.
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
;
; Usage:
;
; $idl AMSR_E_L3_DailyLand_V06_20050118.hdf.idl
;
; Tested under: IDL 8.6.0
; Last updated: 2018-10-16

; Define file name, grid name, and data field.
FILE_NAME='AMSR_E_L3_DailyLand_V06_20050118.hdf'
GRID_NAME='Ascending_Land_Grid'
DATAFIELD_NAME='A_TB36.5H (Res 1)'

; See the specification [1]. The file doesn't have unit attribute on dataset.
UNIT = 'K'

; Open file.
file_id = EOS_GD_OPEN(FILE_NAME)

; Attach grid.
grid_id = EOS_GD_ATTACH(file_id, GRID_NAME)

; Retrieve data.
status = EOS_GD_READFIELD(grid_id, DATAFIELD_NAME, data)

; Read fill value.
status = EOS_GD_GETFILLVALUE(grid_id, DATAFIELD_NAME, fillvalue)

; Detach grid.
status = EOS_GD_DETACH(grid_id)

; Close file.
status = EOS_GD_CLOSE(file_id)

; Convert data type.
dataf=float(data)

; Process fill value.
idx=where(data eq fillvalue, cnt)
if cnt gt 0 then dataf[idx]=!Values.F_NAN

; Multiply scale by 0.1 to get Kelvin according to [1].
dataf = dataf * 0.1

; Get max and min value of data.
datamin = min(dataf, /NAN)
datamax = max(dataf, /NAN)


; The file contains CEA projection. We need to use eosdump to generate 
; 1D lat and lon. For information on how to obtain the lat/lon data, 
; check [2].

; Retrieve dimension size of datadimsize=size(data,/dim)
dimsize=size(data,/dim)
numlat=dimsize(1)
numlon=dimsize(0)

; Read lat/lon generated by dump tool.
openr,1,'lat_AMSR_E_L3_DailyLand_V06_20050118_Ascending_Land_Grid.output'
lat=FltArr(numlat)
READF,1,lat

openr,2,'lon_AMSR_E_L3_DailyLand_V06_20050118_Ascending_Land_Grid.output'
lon=FltArr(numlon)
READF,2,lon

; Generate the plot using CEA projection [3].
m = MAP('Cylindrical equal area', TITLE=file_name, FONT_SIZE=9, /BUFFER)
ct = COLORTABLE(72, /reverse)

t1 = TEXT(0.35, 0.3, datafield_name)
c1 = CONTOUR(dataf, lon, lat, /FILL, OVERPLOT=m, $
             RGB_TABLE=ct, $
             GRID_UNITS='degrees', POSITION=[0.1, 0.1, 0.83, 0.9])
mc = MAPCONTINENTS()
units = 'K'
cb = COLORBAR(target = c1,/BORDER, ORIENTATION=1, TEXTPOS=1, $
              POSITION=[0.85,0.3,0.87,0.7], TITLE=units)
png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT


; Reference
;
; [1] https://nsidc.org/data/ae_land3
; [2] http://hdfeos.org/zoo/note_non_geographic.php
; [3] https://www.harrisgeospatial.com/docs/wcsxy2sph.html
