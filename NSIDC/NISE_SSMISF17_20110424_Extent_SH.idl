; This example code illustrates how to access and visualize an NSIDC MODIS 25km
; LAMAZ (EASE) Grid file in IDL.  EASE stands for "Equal-Area, Spherical Earth"
; See reference [1], below.  The EASE map projection is the same as the Lambert
; Azimuthal projection, known as "LAMAZ" to EOS.

; If you have any questions, suggestions, comments on this example, please use
;  the HDF-EOS Forum (http://hdfeos.org/forums).
;
; If you would like to see an example of any other NASA HDF/HDF-EOS data
;  product that is not listed in the HDF-EOS Comprehensive Examples page
;  (http://hdfeos.org/zoo), feel free to contact us at
;  eoshelp@hdfgroup.org or post it at the HDF-EOS Forum
; (http://hdfeos.org/forums).

; Example HDF File source:
; ftp://ftp.hdfgroup.uiuc.edu/pub/outgoing/NASAHDF/
;       NISE_SSMISF17_20110424.HDFEOS
; Original File Source:
; ftp://n4ftl01u.ecs.nasa.gov/SAN/OTHR/NISE.004/2011.04.24/
;       NISE_SSMISF17_20110424.HDFEOS
; File metadata:
; ftp://n4ftl01u.ecs.nasa.gov/SAN/OTHR/NISE.004/2011.04.24/
;       NISE_SSMISF17_20110424.HDFEOS.xml
; Pre-rendered browse images:
; ftp://n4ftl01u.ecs.nasa.gov/SAN/OTHR/NISE.004/2011.04.24/
;       NISE_SSMISF17_20110424.1.jpg and NISE_SSMISF17_20110424.2.jpg

; Define  the file name, grid name, and data field name.
FILE_NAME='NISE_SSMISF17_20110424.HDFEOS'
GRID_NAME='Southern Hemisphere'
DATAFIELD_NAME='Extent'

; This data file has no 'units' attribute
UNIT='none'

; Open file via the EOS_GD interface.
file_id = EOS_GD_OPEN(FILE_NAME)

; Attach to the named grid.
grid_id = EOS_GD_ATTACH(file_id, GRID_NAME)

; Retrieve data via the EOS_GD interface
status = EOS_GD_READFIELD(grid_id, DATAFIELD_NAME, data)

; Detach from the grid.
status = EOS_GD_DETACH(grid_id)

; Close the EOS_GD interface to the file.
status = EOS_GD_CLOSE(file_id)

; The map projection for the data grid is Lambert Azimuthal (LAMAZ)
; In this example, we used the EOS2 Dumper generate 1D lat and lon.
; For information on how to obtain the lat/lon data, see reference [2], below.

; get the data field dimensions
dimsize=size(data,/dim)
numlat=dimsize(0)
numlon=dimsize(1)

; Open the ASCII geolocation file and read the latitude data into a 1-D array.
LAT_FILENAME='lat_NISE_SSMISF17_20110424.Southern_Hemisphere.output'
openr,1,LAT_FILENAME
size=numlat*numlon
ValInlat=FltArr(size)
READF,1,ValInlat
close,1

; Open the ASCII geolocation file and read the longitude data into a 1-D array.
LON_FILENAME='lon_NISE_SSMISF17_20110424.Southern_Hemisphere.output'
openr,2,LON_FILENAME
size=numlat*numlon
ValInlon=FltArr(size)
READF,2,ValInlon
close,2

; Reform the lat/lon arrays to 2-D grids of same dimensions as the data field.
lat=FINDGEN(numlat,numlon)
lat=Reform(ValInlat,numlat,numlon)

lon=FINDGEN(numlat,numlon)
lon=Reform(ValInlon,numlat,numlon)

;  The following key information for color table is obtained from the
; data field's "Key" attribute. You can check it using HDFView.
;
; data_grid_key = Data Value     Parameter
;                 0              snow-free land
;                 1-100          sea ice concentration percentage
;                 101            permanent ice (Greenland, Antarctica)
;                 102            not used
;                 103            dry snow
;                 104            wet snow
;                 105-251        not used
;                 252            mixed pixels at coastlines
;                               (unable to reliably apply microwave algorithm)
;                 253            suspect ice value
;                 254            corners(undefined)
;                 255            ocean

; Construct a color map which is close to the "Image Gallery" of NSIDC [3].
; The first black (0,0,0) entry is for background(bg).
; Key: bg, 0,  1,    21,  41,  61,  81, 101, 103, 104, 252, 253, 255
red =  [0, 0,  0,     0,   0,   0,   0,  63, 255, 127,  25,   0,   0]
green= [0, 63, 0,    63, 127, 191, 255,   0, 255, 127,  25,   0,   0]
blue=  [0, 0,  255, 255, 255, 255, 255,  63, 255, 127,  25,   0, 127]
C_levels=[0, 1, 21, 41, 61, 81, 101, 103, 104, 252, 253, 255]
levels = 12

; Generate the plot.
DEVICE, DECOMPOSED=0
; Load the custom color table.
TVLCT, red, green, blue

WINDOW, Title='FIELD:' +  DATAFIELD_NAME +'  '+'UNIT:'+ unit

; Limit the plot to the Southern Hemisphere.
;
; This LAMAZ projection data spans over -90 to 90 and -180 and 180.
; To get a zoomed effect, MAP_SET LIMIT option the lat max to -30
latmin=-90
latmax=-30
lonmin=-180
lonmax=180

; LAMBERT Azimuthal Projection with origin at the South pole (-90 lat)
; The /ISOTROPIC option makes the plot square.
MAP_SET, /LAMBERT, -90, 0, 0, /ISOTROPIC, /CONTINENTS, XMARGIN=5, YMARGIN=5, $
 POSITION=[0.05, 0.05, 0.82, 0.82], LIMIT=[latmin, lonmin, latmax, lonmax]
; Generate the filled contour plot
CONTOUR, data, lon, lat, /OVERPLOT, /CELL_FILL, LEVELS=C_levels, $
  C_Colors=Indgen(levels)+1

; Don't use /BOX_GRID option for polar view.
MAP_GRID, /LABEL
MAP_CONTINENTS
XYOuts, 0.35, 0.84, /Normal, DATAFIELD_NAME, $
  Charsize=1.25, Alignment=0.0
XYOuts, 0.8, 0.84, /Normal, 'UNIT:' + unit, $
  Charsize=1.25, Alignment=0.0
XYOuts, 0.5, 0.92, /Normal, FILE_NAME, $
  Charsize=1.75,  Alignment=0.5

;  The following code is prepared for colorbar.
;
;   If you require colorbar in your plot, you could download
; "Dr. Fanning's Coyote Library" from reference [4], below.
; Make a directory named coyote somewhere on your machine,
; and extract the Coyote files into it.
;
;   If color bar is not not necessary for your plot, you can ignore this
; step and add comment character ';' ahead of coding.
;
;    Add the coyote directory you create on your machine to your IDL
;  path like below:
;
; !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH

; Let's assume that you've installed coyote library under the current working
; directory that this code exists.
!PATH=Expand_Path('+/mnt/hdf/s-dickson/lib/coyote/')+':'+!PATH

; The following key information in "C_labels" is obtained from the
; data field's "Key" attribute.
;
; You can check it using HDFView.
C_labels=['snow-free land', '1-20pct Sea Ice', '21-40pct Sea Ice',$
            '41-60pct Sea Ice', '61-80pct Sea Ice', '81-100pct Sea Ice',$
            'permanent ice', 'dry snow', 'wet snow',$
            'mixed pixels at coastlines', 'suspect ice value', 'ocean']

; Plot the discrete color bar.
DCBAR, NCOLORS=levels, BOTTOM=1,  /Vertical, LABELS=C_labels, $
  Position=[0.8,0.1,0.82,0.8]

; References
;
; [1] Near-Real-Time SSM/I-SSMIS EASE-Grid
;     Daily Global Ice Concentration and Snow Extent
;     http://nsidc.org/data/nise1.html
; [2] http://hdfeos.org/zoo/note_non_geographic.php
; [3] browse images:
;     ftp://n4ftl01u.ecs.nasa.gov/SAN/OTHR/NISE.004/2011.04.24/
;           NISE_SSMISF17_20110424.1.jpg and NISE_SSMISF17_20110424.2.jpg
; [4] http://www.dfanning.com/documents/programs.html

