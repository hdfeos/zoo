;
;  This example code illustrates how to access and visualize NSIDC AMSR_E
; Grid file in IDL.
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
;
; Usage:
;
; $idl AMSR_E_L3_DailyOcean_V04_20020619.hdf.idl
;
; Tested under: IDL 8.6.0
; Last updated: 2018-11-07

; Define file name, grid name, and data field.
FILE_NAME='AMSR_E_L3_DailyOcean_V04_20020619.hdf'
GRID_NAME='GlobalGrid'
DATAFIELD_NAME='High_res_cloud'

; Open file.
file_id = EOS_GD_OPEN(FILE_NAME)

; Attach grid.
grid_id = EOS_GD_ATTACH(file_id, GRID_NAME)

; Retrieve data.
status = EOS_GD_READFIELD(grid_id, DATAFIELD_NAME, data)
status = EOS_GD_GRIDINFO(grid_id, xdimsize, ydimsize, upleft, lowright)
status = EOS_GD_PIXREGINFO(grid_id, pixregcode)

; Detach grid.
status = EOS_GD_DETACH(grid_id)

; Close file.
status = EOS_GD_CLOSE(file_id)


; Retrieve "Unit" and "Scale" attributes.
;
; Since HDF-EOS2 specific IDL routines (e.g., EOS_GD_...()) cannot access
; data field attributes, we need to open it with generic HDF4 specific IDL
; routines (e.g., HDF_SD...()).
newFileID=HDF_SD_START(FILE_NAME, /READ)
index = HDF_SD_NAMETOINDEX(newFileID, DATAFIELD_NAME)
thisSdsID=HDF_SD_SELECT(newFileID, index)
units_index=HDF_SD_ATTRFIND(thisSdsID, 'Unit')
HDF_SD_ATTRINFO, thisSdsID, units_index, DATA=units
scalefactor_index=HDF_SD_ATTRFIND(thisSdsID, 'Scale')
HDF_SD_ATTRINFO, thisSdsID, scalefactor_index, DATA=scale_factor
HDF_SD_END, newFileID

; Compute lat / lon data.
; 
; We assume that HDFE_CENTER is 0 by default. See GDdefpixreg() in 
; "HDF-EOS Library Users Guide for the ECS Project Volume 2: Function
; Reference Guide".
IF (pixregcode EQ 1) THEN PRINT, 'ERROR:Pixel is not centered.' 
IF (pixregcode EQ 1) THEN EXIT

; Pixel must be centered.
offsetX = 0.5
offsetY = 0.5

; We need to readjust the limits of latitude and longitude. 
; HDF-EOS is using DMS(DDDMMMSSS.SS) format to represent degrees.
; To calculate the lat and lon in degrees, one needs to convert
; minutes and seconds into degrees. 

; The following is the detailed description on how to calculate the
; latitude  and longitude range based on lowright and upleft.
; One should observe the fact that 1 minute is 60 seconds and 1 degree 
; is 60 minutes. 

; First calculate the difference of .SS between lowright and upleft.
; Since the resulting number is very large, you need to specify /L64.
lowright_ss= lowright*100-FLOOR(lowright, /L64)*100
upleft_ss = upleft*100-FLOOR(upleft, /L64)*100;
dss = lowright_ss - upleft_ss;

; Calculate the difference of SSS between lowright and upleft.
lowright_s = floor(lowright) MOD 1000
upleft_s = floor(upleft) MOD 1000
ds = lowright_s - upleft_s +dss/100;

; Calculate the difference of MMM between lowright and upleft.
lowright_m = floor(lowright/1000) MOD 1000
upleft_m = floor(upleft/1000) MOD 1000
dm = lowright_m-upleft_m +ds/60;

; Calculate the difference of DDD between lowright and upleft.
lowright_d = floor(lowright/1000000)
upleft_d = floor(upleft/1000000)
dd = lowright_d-upleft_d+dm/60

lat_limit = dd(1);
lon_limit = dd(0);

; We need to calculate the grid space interval between two adjacent points.
scaleX = lon_limit/xdimsize
scaleY = lat_limit/ydimsize

lon_value= FINDGEN(xdimsize)
lat_value= FINDGEN(ydimsize)
for i=0,xdimsize-1 do lon_value(i) = (i+offsetX)*(scaleX) + upleft_d(0);
for j=0,ydimsize-1 do lat_value(j) = (j+offsetX)*(scaleY) + upleft_d(1);

lon = float(lon_value)
lat = float(lat_value)

; Compute data.
; Convert data type since it is 16 bit integer.
; Type conversion is necessary to process fill value.
dataf=float(data)

; Apply scale factor.
dataf = scale_factor(0) * dataf

; Read fill value from data directly using HDFView.
fillvalue = -9999
; Process fill value.
idx=where(data eq fillvalue, cnt)
if cnt gt 0 then dataf[idx] = !Values.F_NAN
;print, dataf

; Get max and min value of data.
datamin = min(dataf, /NAN)
datamax = max(dataf, /NAN)

; Generate the plot.
m = MAP('Geographic', TITLE=file_name, FONT_SIZE=9, /BUFFER)
ct = COLORTABLE(72, /reverse)

t1 = TEXT(0.35, 0.05, DATAFIELD_NAME)
c1 = CONTOUR(dataf, lon, lat, /FILL, OVERPLOT=m, $
             RGB_TABLE=ct, $
             GRID_UNITS='degrees', POSITION=[0.1, 0.1, 0.83, 0.9])
mc = MAPCONTINENTS()
cb = COLORBAR(TARGET=c1, /BORDER, ORIENTATION=1, TEXTPOS=1, POSITION=[0.85,0.2,0.87,0.8], TITLE=units)
png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT


