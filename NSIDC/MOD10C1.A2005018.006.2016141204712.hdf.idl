;
;  This example code illustrates how to access and visualize NSIDC MODIS
; Grid file in IDL. This Grid file uses Sinusoidal projection.
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
;
; Usage:
;
; $idl MOD10C1.A2005018.006.2016141204712.hdf.idl
;
; Tested under: IDL 8.6.0
; Last updated: 2019-02-23

FILE_NAME='MOD10C1.A2005018.006.2016141204712.hdf'
GRID_NAME='MOD_CMG_Snow_5km'
DATAFIELD_NAME='Day_CMG_Snow_Cover'

; Open file.
file_id = EOS_GD_OPEN(FILE_NAME)

; Attach grid.
grid_id = EOS_GD_ATTACH(file_id, GRID_NAME)

; Retrieve data.
status = EOS_GD_READFIELD(grid_id, DATAFIELD_NAME, data)
status = EOS_GD_GRIDINFO(grid_id, xdimsize, ydimsize, upleft, lowright)
status = EOS_GD_PIXREGINFO(grid_id, pixregcode)

; Detach Grid.
status = EOS_GD_DETACH(grid_id)

; Close file.
status = EOS_GD_CLOSE(file_id)

; Retrieve long_name, Key, fillvalue and units attributes.
newFileID=HDF_SD_START(FILE_NAME, /READ)

DATAFIELD_NAME='Day_CMG_Snow_Cover'
index = HDF_SD_NAMETOINDEX(newFileID, DATAFIELD_NAME)

thisSdsID=HDF_SD_SELECT(newFileID, index)

long_name_index=HDF_SD_ATTRFIND(thisSdsID, 'long_name')
HDF_SD_ATTRINFO, thisSdsID, long_name_index, data=long_name

units_index=HDF_SD_ATTRFIND(thisSdsID, 'units')
HDF_SD_ATTRINFO, thisSdsID, units_index, DATA=units

key_index=HDF_SD_ATTRFIND(thisSdsID, 'Key')
HDF_SD_ATTRINFO, thisSdsID, key_index, data=key

HDF_SD_END, newFileID

; Check "Key" attribute to plot the data correctly. It will print:
; 
; 0-100=percent of snow in cell, 107=lake ice, 111=night,
; 
; 250=cloud obscured water,253=data not mapped, 254=water mask, 255=fill
; PRINT, Key

; Check what discrete "Key" values are used to determine color table
; and discrete color bar.
; data_used = data[UNIQ(data, SORT(data))] 
; PRINT, data_used

; Construct a discrete data for plot.
data_discrete = data

; Put 1%-99% data under one grey label.
data_discrete[WHERE(data GT 0 AND data LT 100 )] = 99

; Create an array for levels in plot.
data_unique = data_discrete[UNIQ(data_discrete, SORT(data_discrete))] 
; PRINT, data_unique

; Save the  number of levels for discrete color bar.
dimsize_data_unique = size(data_unique,/dim)
levels = dimsize_data_unique(0)
; PRINT, levels
; Compute lat / lon data.
; 
; We assume that HDFE_CENTER is 0 by default. See GDdefpixreg() in 
; "HDF-EOS Library Users Guide for the ECS Project Volume 2: Function
; Reference Guide".
IF (pixregcode EQ 1) THEN PRINT, 'ERROR:Pixel is not centered.' 
IF (pixregcode EQ 1) THEN EXIT

; Pixel must be centered.
offsetX = 0.5
offsetY = 0.5

; We need to readjust the limits of latitude and longitude. 
; HDF-EOS is using DMS(DDDMMMSSS.SS) format to represent degrees.
; To calculate the lat and lon in degrees, one needs to convert
; minutes and seconds into degrees. 

; The following is the detailed description on how to calculate the
; latitude  and longitude range based on lowright and upleft.
; One should observe the fact that 1 minute is 60 seconds and 1 degree 
; is 60 minutes. 

; First calculate the difference of .SS between lowright and upleft.
; Since the resulting number is very large, you need to specify /L64.
lowright_ss= lowright*100-FLOOR(lowright, /L64)*100
upleft_ss = upleft*100-FLOOR(upleft, /L64)*100;
dss = lowright_ss - upleft_ss;

; Calculate the difference of SSS between lowright and upleft.
lowright_s = floor(lowright) MOD 1000
upleft_s = floor(upleft) MOD 1000
ds = lowright_s - upleft_s +dss/100;

; Calculate the difference of MMM between lowright and upleft.
lowright_m = floor(lowright/1000) MOD 1000
upleft_m = floor(upleft/1000) MOD 1000
dm = lowright_m-upleft_m +ds/60;

; Calculate the difference of DDD between lowright and upleft.
lowright_d = floor(lowright/1000000)
upleft_d = floor(upleft/1000000)
dd = lowright_d-upleft_d+dm/60

lat_limit = dd(1);
lon_limit = dd(0);

; We need to calculate the grid space interval between two adjacent points.
scaleX = lon_limit/xdimsize
scaleY = lat_limit/ydimsize

lon_value= FINDGEN(xdimsize)
lat_value= FINDGEN(ydimsize)
for i=0,xdimsize-1 do lon_value(i) = (i+offsetX)*(scaleX) + upleft_d(0);
for j=0,ydimsize-1 do lat_value(j) = (j+offsetX)*(scaleY) + upleft_d(1);

lon = float(lon_value)
lat = float(lat_value)
; Construct a color table.
; Key      R  G   B    Name
; ==========================
;  0%     0   100 0    dark green
;  1-99%  127 127 127  grey
;  100%   255 255 255  white
;  107    255 176 255  pink  
;  111    0   0   0    black
;  237    0   0   255  blue
;  239    0   0   205  medium blue
;  250    100 200 255
;  253    255 0   255  magenta
;  255    138 42  226  blue violet
;
;  We added two more (0% and 1-99%) entries for ice coverage to get better
;  image.

; Remap based on unique values.
FOR i=0,levels-1 DO data_discrete[WHERE(data_discrete EQ data_unique[i])] = i+1
index = FINDGEN(levels)
; PRINT, index
ct = COLORTABLE([[0,100,0], $   ; 0%
                 [127,127,127], $ ; 1-99%
                 [255,255,255], $ ; 100%
                 [255,176,255], $ ; lake ice
                 [0,0,0], $ ; night
                 [0,0,255], $ ; inland water
                 [0,0,205], $ ; ocean
                 [100,100,255], $ ; cloud obscured water
                 [0,0,205], $ ; data not mapped
                 [138,43,226]], $ ; fill
                 NCOLORS=levels, /TRANSPOSE)

; Generate a plot.
m = MAP('Geographic', TITLE=file_name, /BUFFER)

c1 = CONTOUR(data_discrete, $
             lon, $
             lat, $
             BACKGROUND_COLOR=[0,0,0], $
             N_LEVELS = levels, $
             OVERPLOT=m, $
             RGB_INDICES=index, $
             C_VALUE=index, $
             /FILL, $
             /BUFFER, $
             RGB_TABLE=ct, $
             GRID_UNITS='degrees', POSITION=[0.1, 0.1, 0.83, 0.9])
t1 = TEXT(0.35, 0.01, long_name)

; We need a custom discrete colorbar.
cb = COLORBAR(RGB_TABLE=ct, RANGE=[0,10], ORIENTATION=1, BORDER=1, $
              TICKVALUES=index+0.5, $
              TICKNAME=['0% snow', '1-99% snow',  '100% snow', 'lake ice', $
                        'night', 'inland water', 'ocean', $
                        'cloud obsc.!Cwater',$
                        'data not!Cmapped', 'fill'], $
              TEXTPOS=1, POSITION=[0.85,0.2,0.87,0.8], TITLE=units)
png = FILE_NAME + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT

