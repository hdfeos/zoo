;  This example code illustrates how to access and visualize LaRC
;  CERES ISCCP HDF4 file in IDL.
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; Refer to: CER_ISCCP-D2like-GEO_Composite_Beta1_023031.200510.hdf
; DataField name: Effective Temperature - M


; Open file
file_name='CER_ISCCP-D2like-GEO_Composite_Beta1_023031.200510.hdf'
file_id=HDF_SD_START(file_name, /READ)

; Define data field.
datafield_name='Effective Temperature - M'
index=HDF_SD_NAMETOINDEX(file_id,datafield_name)

; Retrieve data.
sds_id=HDF_SD_SELECT(file_id, index)
HDF_SD_GETDATA, sds_id, data

; Convert the 3D data into 2D data at "thin->thick..." dimension = 0.
data2D=data[*,*,0]
data2D=reform(data2D)

; Retrieve fillvalue and units attribute values.
units_index=HDF_SD_ATTRFIND(sds_id, 'units')
HDF_SD_ATTRINFO, sds_id, units_index, DATA=units

fillvalue_index=HDF_SD_ATTRFIND(sds_id, '_FillValue')
HDF_SD_ATTRINFO, sds_id, fillvalue_index, DATA=fillvalue

; Retrieve lat/lon.
latitude_name='Colatitude - M'
index=HDF_SD_NAMETOINDEX(file_id,latitude_name)

sds_id=HDF_SD_SELECT(file_id, index)
HDF_SD_GETDATA, sds_id, Colat

lat=Colat[0,*,0]
lat=reform(lat)
lat=90-lat

longitudename='Longitude - M'
index=HDF_SD_NAMETOINDEX(file_id,longitudename)

sds_id=HDF_SD_SELECT(file_id, index)
HDF_SD_GETDATA, sds_id, lon

lon=lon[*,0,0]
lon=reform(lon)
lon(0:179)=lon(0:179)-360

; Close file.
HDF_SD_END, file_id


; Convert data type.
dataf=FLOAT(data2D)

; Process fill value.
idx=WHERE(data2D EQ fillvalue(0), cnt)
if cnt gt 0 then dataf[idx]=!Values.F_NAN

; Get max and min value of data.
datamin=min(dataf, /NAN)
datamax=max(dataf, /NAN)

; Generate the plot.
levels = 254
DEVICE, DECOMPOSED=0
LoadCT, 33, NCOLORS=levels, BOTTOM=1
WINDOW, Title='FIELD:' + datafield_name + '       UNIT:'+units
MAP_SET, /GRID, /CONTINENTS, XMARGIN=5, YMARGIN=5, $
         POSITION=[0.05, 0.05, 0.82, 0.82]
CONTOUR, dataf, lon, lat, /OVERPLOT, /CELL_FILL, C_Colors=Indgen(levels)+3, $
         Background=1, NLEVELS=levels, Color=Black
MAP_GRID, /BOX_AXES, COLOR=255
MAP_CONTINENTS, COLOR=255

; Draw title and unit. See [1] for the meaningful description of data set.
XYOuts, 0.05, 0.86, /Normal, $
        'FIELD:Monthly Mean Effective Temperature of Cumulus', $
        Charsize=1.25, color=black, Alignment=0.0
XYOuts, 0.95, 0.86, /Normal, 'UNIT:' + units, $ 
  Charsize=1.25, Color=black, Alignment=1.0
XYOuts, 0.5, 0.94, /Normal, FILE_NAME, $
  Charsize=1.25, Color=black, Alignment=0.5

;   The following code is prepared for colorbar. 
;
;   If you require colorbar in your plot, you could download 
; "Dr. Fanning's Coyote Library" from [2]. Make a directory named
; coyote somewhere on your machine, and extract the Coyote files into
; it. 
;
;   If color bar is not not necessary for your plot, you can ignore this
; step and add comment character ';' ahead of coding. 
;
;    Add the coyote directory you create on your machine to your IDL
;  path like below: 
;
; !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH

; Let's assume that you've installed coyote library under the current working
; directory that this code exists.
!PATH=Expand_Path('+coyote/')+':'+!PATH

;  The following code assumes that you've already download and installed 
; "Dr. Fanning's Coyote Library" and add the coyote directory above. 
;
;  If you don't need color bar in your plot, you can ignore this step
;  by adding comment character ';' at the beginning of the code.
COLORBAR, Range=[datamin, datamax], Ncolors=levels, /Vertical, $ 
  Position=[0.9,0.05,0.94,0.8]

; Write JPEG image file.
im = TVRD(TRUE=3)
jpg = 'CER_ISCCP-D2like-GEO_Composite_Beta1_023031.200510_Effective_Temperature_-_M_thin_thick0_Monthly_Hourly_Avgs0.idl.JPG' 
WRITE_JPEG, jpg, im, QUALITY=100, TRUE=3

; References
;
; [1] http://ceres.larc.nasa.gov/documents/DPC/DPC_current/pdfs/DPC_all.pdf
; [2] http://www.dfanning.com/documents/programs.html
