;
; This example code illustrates how to access and visualize LaRC ASDC MOPITT 
; version 6 Grid file in IDL.
;
;  If you have any questions, suggestions, comments on this example, please use
; the HDF-EOS Forum (http://hdfeos.org/forums). 
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS data 
; product that is not listed in the HDF-EOS Comprehensive Examples page 
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org or 
; post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; Usage:
;
;  %idl MOP03T-20131129-L3V4.2.1.he5.idl
;
; Tested under: IDL 8.2.3
; Last updated: 2014-01-17

; Open file.
  file_name = 'MOP03T-20131129-L3V4.2.1.he5'
  file_id = H5F_OPEN(file_name)

; Read latitude.
  datafield_name = '/HDFEOS/GRIDS/MOP03/Data Fields/Latitude'
  data_id = H5D_OPEN(file_id,datafield_name)
  lat = H5D_READ(data_id)

; Read longitude.
  datafield_name = '/HDFEOS/GRIDS/MOP03/Data Fields/Longitude'
  data_id = H5D_OPEN(file_id,datafield_name)
  lon = H5D_READ(data_id)

; Read variable
  datafield_name = '/HDFEOS/GRIDS/MOP03/Data Fields/RetrievedSurfaceTemperatureDay'
  data_id = H5D_OPEN(file_id,datafield_name)

  dataspace_id = H5D_GET_SPACE(data_id)
  dims = H5S_GET_SIMPLE_EXTENT_DIMS(dataspace_id)
  dims = float(dims)

  data = H5D_READ(data_id)

; Get units.
  units_id = H5A_OPEN_NAME(data_id, 'units')
  units = H5A_READ(units_id)

; Get fill value.
  fillvalue_id = H5A_OPEN_NAME(data_id,'_FillValue')
  fillvalue = H5A_READ(fillvalue_id)

; Get long name attribute.
  long_name_id = H5A_OPEN_NAME(data_id,'long_name')
  long_name = H5A_READ(long_name_id)

; Convert data type.
  dataf = float(data)
  fillvaluef = float(fillvalue(0))

  H5A_Close, fillvalue_id
  H5D_Close, data_id

; Process missing value, convert dataf that are equal to fillvaluef to NaN.
  idx = where(dataf eq fillvaluef(0), cnt)
  if cnt gt 0 then dataf[idx] = !Values.F_NAN


; Get max and min value of data for color bar.
  datamin = MIN(dataf, /NAN)
  datamax = MAX(dataf, /NAN)
  

; Tranpose data.
  dataf = TRANSPOSE(dataf)

; Generate the plot.
  SET_PLOT, 'Z'
  levels = 254
  DEVICE, SET_RESOLUTION=[800,600], SET_PIXEL_DEPTH=24, DECOMPOSED=0
  LOADCT, 33, Ncolors=levels, Bottom=1
  MAP_SET, /GRID, /CONTINENTS,XMARGIN=5, YMARGIN=5, $
    POSITION=[0.05, 0.06, 0.82, 0.80]
  CONTOUR, dataf, lon, lat, /OVERPLOT, /CELL_FILL, C_Colors=Indgen(levels)+3, $
           Background=1, NLEVELS=levels, Color=Black
  MAP_GRID, /BOX_AXES, COLOR=255
  MAP_CONTINENTS, COLOR=255

; Draw file name, field name, and unit.
  XYOUTS, 0.05, 0.86, /NORMAL, 'FIELD:' + long_name, $
    CHARSIZE=1.25,  ALIGNMENT=0.0
  XYOUTS, 0.94, 0.86, /NORMAL, 'UNIT:' + units, $ 
    CHARSIZE=1.25,  ALIGNMENT=1.0
  XYOUTS, 0.43, 0.92, /NORMAL, file_name, $
    CHARSIZE=1.75,  ALIGNMENT=0.5

; The following code is prepared for colorbar. 
;
; If you want to have a color bar in your plot, please download 
; "Dr. Fanning's Coyote Library" from [1]. Make a directory named
; coyote somewhere on your machine, and extract the Coyote library files into
; it. 
;
; If color bar is not not necessary for your plot, you can ignore this
; step and add comment character ';' at the beginning of the rest of codes.
;
; Add the coyote directory that you created on your machine to your IDL
; path like below: 
;
; !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH

; We assume that the coyote library is installed under the current working
; directory that this code exists.
  !PATH=EXPAND_PATH('+coyote/')+':'+!PATH

; The following code assumes that you've already download and installed 
; "Dr. Fanning's Coyote Library" and add the coyote directory above. 
;
; If you don't need a color bar in your plot, you can ignore this step
; by adding comment character ';' at the beginning of the code.

  COLORBAR, RANGE=[datamin, datamax], NCOLORS=levels, /VERTICAL, $
    POSITION=[0.9,0.08,0.94,0.8]

; Write PNG image file.
  im = TVRD(TRUE=1)
  WRITE_PNG, file_name+'.idl.png', im
  EXIT


; Reference
;
; [1] http://www.dfanning.com/documents/programs.html
