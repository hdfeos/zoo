;  This example code illustrates how to access and visualize LaRC MISR
;Grid file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; Define file name, grid name, and data field
FILE_NAME='MISR_AM1_TC_ALBEDO_P223_O056884_F05_0011.hdf'
GRID_NAME='ReflectingLevelParameters_2.2_km'
DATAFIELD_NAME='AlbedoLocal'

; Open file.
file_id = EOS_GD_OPEN(FILE_NAME)

; Retrieve data.
grid_id = EOS_GD_ATTACH(file_id, GRID_NAME)
status = EOS_GD_READFIELD(grid_id, DATAFIELD_NAME, data)
status = EOS_GD_GETFILLVALUE(grid_id, DATAFIELD_NAME, fillvalue)
; Close file.
status = EOS_GD_DETACH(grid_id)
status = EOS_GD_CLOSE(file_id)

; Convert the 4D data to 2D data at SOMBlockDim=50, NBandDim=0.
data=data(0,*,*,50)
data=reform(data)

; Convert data type.
data=float(data)

; Get max and min value of data.
idx=where(data ne fillvalue, cnt)
if cnt gt 0 then datamin=min(data[idx])
if cnt gt 0 then datamax=max(data[idx])

; Convert data that are equal to fillvalue to NaN
idx=where(data eq fillvalue, cnt)
if cnt gt 0 then data[idx]=!Values.F_NAN

; The geolocation values are stored in external files.
; Retrieve dimension size of datadimsize=size(data,/dim).
dimsize=size(data,/dim)
numlat=dimsize(0)
numlon=dimsize(1)

openr,1,'lat_MISR_TC_ALBEDO_P223_F05_lvl50.output'
size=numlat*numlon
ValInlat=FltArr(size)
READF,1,ValInlat

lat=FINDGEN(numlat,numlon)
lat=Reform(ValInlat,numlat,numlon)

openr,2,'lon_MISR_TC_ALBEDO_P223_F05_lvl50.output'
size=numlat*numlon
ValInlon=FltArr(size)
READF,2,ValInlon

lon=FINDGEN(numlat,numlon)
lon=Reform(ValInlon,numlat,numlon)

latmin=min(lat)
latmax=max(lat)
lonmin=min(lon)
lonmax=max(lon)

;   The following code is prepared for colorbar. 
;
;   If you require colorbar in your plot, you could download 
; "Dr. Fanning's Coyote Library" from [2]. Make a directory named
; coyote somewhere on your machine, and extract the Coyote files into
; it. 
;
;   If color bar is not not necessary for your plot, you can ignore this
; step and add comment character ';' ahead of coding. 
;
;    Add the coyote directory you create on your machine to your IDL
;  path like below: 
;
; !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH
;

;  Let's assume that you've installed coyote library under the current working
;  directory that this code exists.
!PATH=Expand_Path('+coyote/')+':'+!PATH


; Start off generating the plot
levels=254
device,decomposed=0
LoadCT,33, Ncolors=levels, Bottom=1
WINDOW, Title='FIELD:'+DATAFIELD_NAME+'  at SOMBlockDim=50 NBandDim=0' +'   '+'UNIT:No Unit'
MAP_SET, /MERCATOR, /CONTINENTS, XMARGIN=5, YMARGIN=5, POSITION=[0.05, 0.05, 0.82, 0.80], LIMIT=[latmin, lonmin, latmax, lonmax]
CONTOUR, data, lon, lat, /OVERPLOT, /Cell_FILL, C_Colors=Indgen(levels)+3, Background=1, NLEVELS=levels, Color=Black
MAP_GRID, /BOX_AXES, COLOR=255
MAP_CONTINENTS, COLOR=255
XYOuts, 0.05, 0.87, /Normal, DATAFIELD_NAME+' at SOMBlockDim=50 NBandDim=0', $
 Charsize=1.25, color=black, Alignment=0.0
; Albedo doesn't have a unit according to the specification [1].
XYOuts, 0.82, 0.87, /Normal, 'UNIT:No Unit', Charsize=1.25, Color=black, Alignment=1.0
XYOuts, 0.43, 0.92, /Normal, file_name, Charsize=1.75, Color=black, Alignment=0.5

;  The following code assumes that you have already downloaded and installed 
; "Dr. Fanning's Coyote Library" and add the coyote directory above. 
;
;  If you don't need color bar in your plot, you can ignore this step
;  by adding comment character ';' at the beginning of the code.
;
;  Since the value ranges 0.0 to 1.0, we need to specify FORMAT='(F10.1)'.
COLORBAR, Range=[datamin, datamax], Ncolors=levels, /Vertical, $
 Position=[0.9,0.05,0.94,0.8], FORMAT='(F10.1)'

; References
;
; [1] https://asdc.larc.nasa.gov/documents/misr/DPS_v50_RevS.pdf
; [2] http://www.dfanning.com/documents/programs.html
