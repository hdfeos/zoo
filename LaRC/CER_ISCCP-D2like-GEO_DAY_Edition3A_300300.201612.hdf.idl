;
;  This example code illustrates how to access and visualize LaRC
;  CERES ISCCP D2like GEO Day Edition3A HDF4 file in IDL.
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
; Usage:
;
;  $idl CER_ISCCP-D2like-GEO_DAY_Edition3A_300300.201612.hdf.idl
;
; Tested under: IDL 8.6.0
; Last updated: 2018-08-09

; Open file
file_name='CER_ISCCP-D2like-GEO_DAY_Edition3A_300300.201612.hdf'
file_id=HDF_SD_START(file_name, /READ)

; Define data field.
datafield_name='Liquid Effective Temperature - Cumulus - M'
index=HDF_SD_NAMETOINDEX(file_id,datafield_name)

; Retrieve data.
sds_id=HDF_SD_SELECT(file_id, index)
HDF_SD_GETDATA, sds_id, data

; Convert the 3D data into 2D data at "thin->thick..." dimension = 0.
data2D=data[*,*,0]
data2D=reform(data2D)

; Retrieve fillvalue and units attribute values.
units_index=HDF_SD_ATTRFIND(sds_id, 'units')
HDF_SD_ATTRINFO, sds_id, units_index, DATA=units

long_name_index=HDF_SD_ATTRFIND(sds_id, 'long_name')
HDF_SD_ATTRINFO, sds_id, long_name_index, DATA=long_name

fillvalue_index=HDF_SD_ATTRFIND(sds_id, '_FillValue')
HDF_SD_ATTRINFO, sds_id, fillvalue_index, DATA=fillvalue

; Retrieve lat/lon.
latitude_name='Colatitude - M'
index=HDF_SD_NAMETOINDEX(file_id,latitude_name)

sds_id=HDF_SD_SELECT(file_id, index)
HDF_SD_GETDATA, sds_id, Colat

lat=Colat[0,*,0]
lat=reform(lat)
lat=90-lat

longitudename='Longitude - M'
index=HDF_SD_NAMETOINDEX(file_id,longitudename)

sds_id=HDF_SD_SELECT(file_id, index)
HDF_SD_GETDATA, sds_id, lon

lon=lon[*,0,0]
lon=reform(lon)
lon(0:179)=lon(0:179)-360

; Close file.
HDF_SD_END, file_id


; Convert data type.
dataf=FLOAT(data2D)

; Process fill value.
idx=WHERE(data2D EQ fillvalue(0), cnt)
if cnt gt 0 then dataf[idx]=!Values.F_NAN

; Get max and min value of data.
datamin=min(dataf, /NAN)
datamax=max(dataf, /NAN)

; Generate the plot.
m = MAP('Geographic', TITLE=file_name, FONT_SIZE=9, /BUFFER)
ct = COLORTABLE(72, /reverse)

; IRREGULAR must be used.
c1 = CONTOUR(dataf, lon, lat, /FILL, /IRREGULAR, OVERPLOT=m, $
             RGB_TABLE=ct, $
             GRID_UNITS='degrees', POSITION=[0.1, 0.1, 0.83, 0.9])
mc = MAPCONTINENTS()
t1 = TEXT(0.01, 0.05, FONT_SIZE=8, long_name)
cb = COLORBAR(TARGET=c1, /BORDER, ORIENTATION=1, $
              TEXTPOS=1, POSITION=[0.85,0.3,0.87,0.7], TITLE=units)
png = file_name + '.idl.png'
c1.save, png, WIDTH=800, HEIGHT=600
EXIT
