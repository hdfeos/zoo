;
;  This example code illustrates how to access and visualize LaRC CALIPSO LIDAR
; Level 2 PSC Mask HDF4 file in IDL. 
;
;  If you have any questions, suggestions, comments on this example, please use
; the HDF-EOS Forum (http://hdfeos.org/forums). 
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS data 
; product that is not listed in the HDF-EOS Comprehensive Examples page 
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org or 
; post it at the HDF-EOS Forum (http://hdfeos.org/forums).
;
; Usage:
;
;  $idl CAL_LID_L2_PSCMask-Standard-V2-00.2021-03-26T00-00-00ZN.hdf.v.idl
;
; Tested under: IDL 8.7.2
; Last updated: 2022-09-26


; Open file.
file_name='CAL_LID_L2_PSCMask-Standard-V2-00.2021-03-26T00-00-00ZN.hdf'

fid = HDF_SD_START(file_name, /READ)

data_name="PSC_Feature_Mask"
index=HDF_SD_NAMETOINDEX(fid, data_name)

; Read data.
sds_id=HDF_SD_SELECT(fid, index)
HDF_SD_GETDATA, sds_id, data

; Read attribute.
fillvalue_index=HDF_SD_ATTRFIND(sds_id, 'fillvalue ')
HDF_SD_ATTRINFO, sds_id, fillvalue_index, data=fillvalue

units_index=HDF_SD_ATTRFIND(sds_id, 'units ')
HDF_SD_ATTRINFO, sds_id, units_index, data=units

; Read lat.
data_name="Latitude"
index=HDF_SD_NAMETOINDEX(fid, data_name)
sds_id=HDF_SD_SELECT(fid, index)
HDF_SD_GETDATA, sds_id, lat

; Read altitude.
data_name="Altitude"
index=HDF_SD_NAMETOINDEX(fid, data_name)
sds_id=HDF_SD_SELECT(fid, index)
HDF_SD_GETDATA, sds_id, alt

; End access to SDS.
HDF_SD_ENDACCESS, sds_id

; Close file.
HDF_SD_END, fid

; Subset data.
s = 0
e = 566
lat = lat[s:e]
datas = data[*, s:e]

; Process fill value.
dataf = FLOAT(datas)
idx=WHERE(datas EQ FIX(fillvalue), cnt)
if cnt gt 0 then dataf[idx]=!Values.F_NAN

longname = 'PSC_Feature_Mask'
ct = COLORTABLE(13)
c1 = CONTOUR(TRANSPOSE(dataf), lat, alt, $
             /FILL, $
             /BUFFER, $
             BACKGROUND_COLOR=[0,0,255], $
             RGB_TABLE=ct, $
             TITLE=file_name, $
             XTITLE='Latitude (degrees_north)', $
             YTITLE='Altitude (km)', $             
             POSITION=[0.1, 0.1, 0.82, 0.8])
cb = COLORBAR(TARGET=c1, /BORDER, ORIENTATION=1, TEXTPOS=1, $
              POSITION=[0.85,0.2,0.87,0.8], TITLE=units)
t1 = TEXT(0.25, 0.805, longname)
png = file_name + '.v.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT
