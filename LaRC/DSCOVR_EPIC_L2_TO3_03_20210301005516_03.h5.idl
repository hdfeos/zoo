;  This example code illustrates how to access and visualize LaRC ADSC
; DSCOVR_EPIC L2 HDF5 Swath file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
; 
; Usage:
;                               
; $idl DSCOVR_EPIC_L2_TO3_03_20210301005516_03.h5.idl
;
; Tested under: IDL 8.7.2
; Last updated: 2021-03-19

; Open file.
file_name='DSCOVR_EPIC_L2_TO3_03_20210301005516_03.h5'
file_id=H5F_OPEN(file_name)

; Read dataset.
data_name='/Ozone'
data_id=H5D_OPEN(file_id, data_name)
data=H5D_READ(data_id)
long_name='Ozone'
units='DU'
fillvalue=-999.0
H5D_CLOSE, data_id

; Read latitude.
lat_name='/Latitude'
lat_id=H5D_OPEN(file_id, lat_name)
lat=H5D_READ(lat_id)
H5D_CLOSE, lat_id

; Read longitude.
lon_name='/Longitude'
lon_id=H5D_OPEN(file_id, lon_name)
lon=H5D_READ(lon_id)
H5D_CLOSE, lon_id
H5F_CLOSE, file_id

; Process missing value, convert data that are equal to fillvalue to NaN
idx = where(data eq fillvalue, cnt)
if cnt gt 0 then data[idx] = !Values.F_NAN

; Pick only valid points for scatter plot.
gidx = WHERE(FINITE(lat), count)
lats = lat[gidx]
lons = lon[gidx]
datas = data[gidx]
datamin = MIN(datas)
datamax = MAX(datas)

; Generate plot.
dimsize=SIZE(data,/dim)
numlat=dimsize(0)
numlon=dimsize(1)

; Pick the middle point.
c_lon = lon[numlat/2, numlon/2]
c_lat = lat[numlat/2, numlon/2]

m = MAP('Orthographic', TITLE=file_name, FONT_SIZE=9, /BUFFER, $
        CENTER_LONGITUDE = c_lon,  CENTER_LATITUDE = c_lat)

; This color table has white color for fill value.
ct = COLORTABLE(13)
t1 = TEXT(0.01, 0.05, FONT_SIZE=8, long_name)
c1 = SCATTERPLOT(lons[*], lats[*], OVERPLOT=m, $
                  MAGNITUDE = datas[*], $
                  RGB_TABLE=ct, $
                  POSITION=[0.1, 0.1, 0.83, 0.9],$
                  /SYM_FILLED, SYMBOL='o', SYM_SIZE=0.1)
mc = MAPCONTINENTS()
cb = COLORBAR(RGB_TABLE=ct, RANGE=[datamin, datamax], /BORDER, ORIENTATION=1, $
              TEXTPOS=1, POSITION=[0.85,0.2,0.87,0.8], TITLE=units)

png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT
