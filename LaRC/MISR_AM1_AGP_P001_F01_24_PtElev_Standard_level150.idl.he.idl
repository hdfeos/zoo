;This example code illustrates how to access and visualize LaRC_MISR Grid file in IDL. 
;If you have any questions, suggestions, comments on this example, please use the HDF-EOS Forum (http://hdfeos.org/forums). 
;If you would like to see an  example of any other NASA HDF/HDF-EOS data product that is not listed in the HDF-EOS Comprehensive Examples page (http://hdfeos.org/zoo), 
;feel free to contact us at eoshelp@hdfgroup.org or post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; Refer to: MISR_AM1_AGP_P001_F01_24.hdf
; DataField name:Blue Radiance/RDQI

; IDL example for reading LaRC_MISR Grid file ---- SOM projection
; Open file
FILE_NAME='MISR_AM1_AGP_P001_F01_24.hdf'
file_id = EOS_GD_OPEN(FILE_NAME)

;define grid name and datafield
GRID_NAME='Standard'
DATAFIELD_NAME='PtElev'

; Retrieve data
grid_id = EOS_GD_ATTACH(file_id, GRID_NAME)
status = EOS_GD_READFIELD(grid_id, DATAFIELD_NAME, data)

; Convert the M-D data to 2D data at SOMBlockDim=150
data=data(*,*,150)
data=reform(data)

; Retrieve lat/lon
DATAFIELD_NAME='GeoLatitude'
grid_id = EOS_GD_ATTACH(file_id, GRID_NAME)
status = EOS_GD_READFIELD(grid_id, DATAFIELD_NAME, lat)

; Convert the M-D lat to 2D lat at SOMBlockDim=150
lat=lat(*,*,150)

DATAFIELD_NAME='GeoLongitude'
grid_id = EOS_GD_ATTACH(file_id, GRID_NAME)
status = EOS_GD_READFIELD(grid_id, DATAFIELD_NAME, lon)

; Convert the M-D lon to 2D lon at SOMBlockDim=150
lon=lon(*,*,150)

; Close file
status = EOS_GD_DETACH(grid_id)
status = EOS_GD_CLOSE(file_id)

;get max and min value of data
datamin=min(data)
datamax=max(data)

;Get vertex value for lat and lon
latmin=min(lat)
latmax=max(lat)
lonmin=min(lon)
lonmax=max(lon)

;The following coding is prepared for colorbar. If you require colorbar in your plot, you could download Dr. Fan's Coyote Library from (http://www.dfanning.com/documents/programs.html). Make a directory named coyote somewhere on your machine, and extract the Coyote files into it. If color bar is not not necessary for your plot, you can ignore this step and add comment character ';' ahead of coding. 
;Add the coyote directory you create on your machine to your IDL path. !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH
!PATH=Expand_Path('+/mnt/hdf/ziying-li/coyote/')+':'+!PATH

; Start off generating the plot
levels=254
device,decomposed=0
LoadCT,33, Ncolors=levels, Bottom=1
WINDOW, Title='PtElev at SOMBlockDim=150'+'  '+'units:'
MAP_SET, /MERCATOR, /CONTINENTS, XMARGIN=5, YMARGIN=5, POSITION=[0.05, 0.05, 0.82, 0.95], LIMIT=[latmin, lonmin, latmax, lonmax]
CONTOUR, data, lon, lat, /OVERPLOT, /FILL, C_Colors=Indgen(levels)+3, Background=1, NLEVELS=levels, Color=Black
MAP_GRID, /BOX_AXES, COLOR=255
MAP_CONTINENTS, COLOR=255
;The following coding is to generate color bar if your already download and install Dr. Fan's Coyote Library and add the coyote directory above. If you don't need color bar in your plot, you can ignore this step and add comment character ';' ahead of coding.
COLORBAR, Range=[datamin, datamax], Ncolors=levels, /Vertical, Position=[0.9,0.1,0.94,0.9]






