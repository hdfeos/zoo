;
;   This example code illustrates how to access and visualize LaRC TES Swath
;  HDF-EOS5 file in IDL. 
;
;  If you have any questions, suggestions, comments on this example, please 
; use the HDF-EOS Forum (http://hdfeos.org/forums). 
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS data 
; product that is not listed in the HDF-EOS Comprehensive Examples page 
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org or 
; post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; Usage:
;
;  %idl TES-Aura_L2-O3-Nadir_r0000011015_F05_07.he5.idl
;
; Tested under: IDL 8.2
; Last updated: 2013-12-09

; Open file.
file_name='TES-Aura_L2-O3-Nadir_r0000011015_F05_07.he5'
file_id=H5F_OPEN(file_name)


; Retrieve data.
datafield_name='/HDFEOS/SWATHS/O3NadirSwath/Data Fields/O3'
data_id=H5D_OPEN(file_id, datafield_name)
data_raw=H5D_READ(data_id)

; Get 'Title' attribute.
title_id=H5A_OPEN_NAME(data_id, 'Title')
long_name=H5A_READ(title_id)
H5A_CLOSE, title_id

; Get 'Units' attribute.
units_id=H5A_OPEN_NAME(data_id, 'Units')
units=H5A_READ(units_id)
H5A_CLOSE, units_id

; Get '_FillValue' attribute.
fill_value_id=H5A_OPEN_NAME(data_id, '_FillValue')
fill_value=H5A_READ(fill_value_id)
H5A_CLOSE, fill_value_id

; Close dataset.
H5D_CLOSE, data_id

; Retrieve lat/lon.
lat_name='/HDFEOS/SWATHS/O3NadirSwath/Geolocation Fields/Latitude'
lat_id=H5D_OPEN(file_id, lat_name)
lat=H5D_READ(lat_id)
H5D_CLOSE, lat_id

lon_name='/HDFEOS/SWATHS/O3NadirSwath/Geolocation Fields/Longitude'
lon_id=H5D_OPEN(file_id, lon_name)
lon=H5D_READ(lon_id)
H5D_CLOSE, lon_id

; Close file.
H5F_CLOSE, file_id

; Convert the 2D data to 1D data.
nlevels = 5;
data1D=data_raw[nlevels, *]
data1D=reform(data1D)

; Convert data type.
dataf=float(data1D)

idx=where(data1D eq fill_value(0), cnt)
if cnt gt 0 then dataf[idx] = !Values.F_NAN

; Compute data min/max for colorbar.
datamin=MIN(dataf)
datamax=MAX(dataf)
dim=SIZE(dataf,/dim)

; Get min/max value of lat and lon for zoomed image.
latmin=min(lat)
latmax=max(lat)
lonmin=min(lon)
lonmax=max(lon)

; Generate the plot.
SET_PLOT, 'Z'
levels = 254
DEVICE, SET_RESOLUTION=[800,600], SET_PIXEL_DEPTH=24, DECOMPOSED=0
LOADCT, 33, NCOLORS=levels, BOTTOM=1


; Plot the trajectory on a zoomed map.
MAP_SET, /GRID, /CONTINENTS, $
         POSITION=[0.05, 0.05, 0.82, 0.82], $ 
         /NOERASE,   LIMIT=[latmin, lonmin, latmax, lonmax] 

; For color.
data = BYTSCL(dataf, /NAN)
MAP_GRID, /BOX_AXES
MAP_CONTINENTS


; PSYM=3 is dot(.) symbol.
FOR i=1,dim(0) DO $
PLOTS, lon(i-1), lat(i-1), COLOR=data(i-1), PSYM=6, SYMSIZE=0.1, THICK=2.0

; Draw file name, field name, and unit.
XYOUTS, 0.05, 0.86, /NORMAL, 'FIELD:' + long_name + $
  ' at nLevels=' + STRING(nlevels, FORMAT='(I1.1)'), $ 
  CHARSIZE=1.25,  ALIGNMENT=0.0
XYOUTS, 0.98, 0.86, /Normal, 'UNIT:' + units, $ 
  CHARSIZE=1.25,  ALIGNMENT=1.0
XYOUTS, 0.43, 0.92, /NORMAL, file_name, $
  CHARSIZE=1.75,  ALIGNMENT=0.5

; The following code is prepared for colorbar. 
;
;   If you want to have a color bar in your plot, please download 
; "Dr. Fanning's Coyote Library" from [1]. Make a directory named
; coyote somewhere on your machine, and extract the Coyote library files into
; it. 
;
;   If color bar is not not necessary for your plot, you can ignore this
; step and add comment character ';' at the beginning of the rest of codes.
;
;   Add the coyote directory that you created on your machine to your IDL
;  path like below: 
;
; !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH
  !PATH=EXPAND_PATH('+coyote/')+':'+!PATH

;  The following code assumes that you've already download and installed 
; "Dr. Fanning's Coyote Library" and add the coyote directory above. 
;
;  If you don't need a color bar in your plot, you can ignore this step
;  by adding comment character ';' at the beginning of the code.
COLORBAR, RANGE=[datamin, datamax], NCOLORS=levels, /VERTICAL, $
 POSITION=[0.94,0.05,0.98,0.8], FORMAT="(e8.1)"

; Write PNG image file.
im = TVRD(TRUE=1)
png = file_name + '.idl.png'   
WRITE_PNG, png, im
EXIT

