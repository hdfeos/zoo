;  This example code illustrates how to access and visualize LaRC MOPITT
; Grid file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; This is an IDL example for reading LaRC MOPITT Grid file.

; Define file name, grid name, and data field.
FILE_NAME='MOP03-20000303-L3V1.0.1.hdf'
GRID_NAME='MOP03'
DATAFIELD_NAME='CO Profiles Day'


; Unit is "ppbv" according to specification [1]. It's not in file.
UNIT = 'ppbv'

; Open file.
file_id = EOS_GD_OPEN(FILE_NAME)

; Attach grid.
grid_id = EOS_GD_ATTACH(file_id, GRID_NAME)

; Retrieve data.
status = EOS_GD_READFIELD(grid_id, DATAFIELD_NAME, data)

; Retrieve longitude.
X_NAME='Longitude'
status = EOS_GD_READFIELD(grid_id, X_NAME, lon)

; Retrieve latitude.
Y_NAME='Latitude'
status = EOS_GD_READFIELD(grid_id, Y_NAME, lat)

; Retrieve pressure.
Z_NAME='Pressure Grid'
status = EOS_GD_READFIELD(grid_id, Z_NAME, press)

; Detach grid.
status = EOS_GD_DETACH(grid_id)

; Close file.
status = EOS_GD_CLOSE(file_id)

; Convert the 3-D data to 2D data at XDim=178.
XDim = 178
data2D=data[*,XDim,*]
data2D=reform(data2D)
data2D=transpose(data2D)


; There's no _FillValue attribute.
; Check fill value directly from the dataset using HDFView and set it manually.
fillvalue = -9999.0

; Convert data type.
data2D=float(data2D)
fillvalue=float(fillvalue)

; Get max and min value of data.
idx=where(data2D ne fillvalue, cnt)
if cnt gt 0 then datamin=min(data2D[idx])
if cnt gt 0 then datamax=max(data2D[idx])

; Process fill value.
idx=where(data2D eq fillvalue, cnt)
if cnt gt 0 then data2D[idx] = !Values.F_NAN


;   The following code is prepared for colorbar. 
;
;   If you require colorbar in your plot, you could download 
; "Dr. Fanning's Coyote Library" from [2]. Make a directory named
; coyote somewhere on your machine, and extract the Coyote files into
; it. 
;
;   If color bar is not not necessary for your plot, you can ignore this
; step and add comment character ';' ahead of coding. 
;
;    Add the coyote directory you create on your machine to your IDL
;  path like below: 
;
; !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH

; Let's assume that you've installed coyote library under the current working
; directory that this code exists.
!PATH=Expand_Path('+coyote/')+':'+!PATH

; Generate the plot.
levels = 200
device, decomposed=0
LoadCT, 33, Ncolors=levels, Bottom=3
WINDOW,Title='FIELD:' + DATAFIELD_NAME $
  +' at '+X_NAME+' = '+ STRING(lon(XDim), FORMAT='(F5.1)') + ' degrees east' $
  + '   ' $
  +'UNIT: ' + UNIT, $
  XSIZE=800
CONTOUR, BYTSCL(data2D, /NAN), lat, press, /Fill, C_Colors=Indgen(levels)+3, $
  Background=1, NLEVELS=levels, Color=Black, $
  XTITLE=Y_NAME+' (degrees)', $
  YTITLE='Pressure Level (hPa)', $
  POSITION=[0.1, 0.1, 0.82, 0.8]

XYOuts, 0.1, 0.82, /Normal, DATAFIELD_NAME $
  +' at '+X_NAME+' = '+ STRING(lon(XDim), FORMAT='(F5.1)') + ' degrees east', $
 Charsize=1.25, color=black, Alignment=0.0
XYOuts, 0.82, 0.82, /Normal, 'UNIT:' + UNIT, $ 
  Charsize=1.25, Color=black, Alignment=1.0
XYOuts, 0.43, 0.92, /Normal, file_name, $
  Charsize=1.75, Color=black, Alignment=0.5

;  The following code assumes that you've already download and installed 
; "Dr. Fanning's Coyote Library" and add the coyote directory above. 
;
;  If you don't need color bar in your plot, you can ignore this step
;  by adding comment character ';' at the beginning of the code.
COLORBAR, Range=[datamin,datamax], Ncolors=levels, /Vertical, $
 Position=[0.9,0.1,0.94,0.8]

; References
;
; [1] http://www.acd.ucar.edu/mopitt/file-spec.shtml#L3
; [2] http://www.dfanning.com/documents/programs.html
