;This example code illustrates how to access and visualize HDF-EOS5 TES L3 Grid file in IDL. 
;If you have any questions, suggestions, comments on this example, please use the HDF-EOS Forum (http://hdfeos.org/forums). 
;If you would like to see an  example of any other NASA HDF/HDF-EOS data product that is not listed in the HDF-EOS Comprehensive Examples page (http://hdfeos.org/zoo), 
;feel free to contact us at eoshelp@hdfgroup.org or post it at the HDF-EOS Forum (http://hdfeos.org/forums).

FILE_NAME='TES-Aura_L3-O3-M2008m03_F01_04.he5'
FILE_ID=H5F_OPEN(FILE_NAME)

DATAFIELD_NAME='/HDFEOS/GRIDS/NadirGrid/Data Fields/TotColDensDataCount'
data_id=H5D_OPEN(FILE_ID,DATAFIELD_NAME)

data=H5D_READ(data_id)

;transpose data and make it be consistent with world map
data=transpose(data)

;retrieve lat and lon
Lat_NAME='/HDFEOS/GRIDS/NadirGrid/Data Fields/Latitude'
lat_id=H5D_OPEN(FILE_ID,Lat_NAME)
lat=H5D_READ(lat_id)

Lon_NAME='/HDFEOS/GRIDS/NadirGrid/Data Fields/Longitude'
lon_id=H5D_OPEN(FILE_ID,Lon_NAME)
lon=H5D_READ(lon_id)

;Get units
units_id=H5A_OPEN_NAME(data_id, 'Units')
units=H5A_READ(units_id)

;Get missing value
missingvalue_id=H5A_OPEN_NAME(data_id,'MissingValue')
missingvalue=H5A_READ(missingvalue_id)

; Convert data type
data=float(data)
missingvalue=float(missingvalue)

H5A_Close, missingvalue_id
H5D_Close, data_id

;get max and min value of data
idx=where(data ne missingvalue(0), cnt)
if cnt gt 0 then datamin=min(data[idx])
if cnt gt 0 then datamax=max(data[idx])

; Process missing value, convert data that are equal to missingvalue to NaN
idx=where(data eq missingvalue(0), cnt)
if cnt gt 0 then data[idx] = !Values.F_NAN

;The following coding is prepared for colorbar. 
;If you require colorbar in your plot, you could download Dr. Fanning's Coyote Library from (http://www.dfanning.com/documents/programs.html). 
;Make a directory named coyote somewhere on your machine, and extract the Coyote files into it. 
;If color bar is not not necessary for your plot, you can ignore this step and add comment character ';' ahead of coding. 
;Add the coyote directory you create on your machine to your IDL path. 
!PATH=Expand_Path('+/mnt/hdf/ziying-li/coyote/')+':'+!PATH

; Start off generating the plot
levels = 254
device, decomposed=0
LoadCT, 33, Ncolors=levels, Bottom=1
WINDOW,Title='TotColDensDataCount'+'  '+'units:'+units, XSIZE=1000
MAP_SET, /GRID, /CONTINENTS, XMARGIN=5, YMARGIN=5, POSITION=[0.05, 0.05, 0.82, 0.95]
CONTOUR, data, lon, lat, /OVERPLOT, /FILL, C_Colors=Indgen(levels)+3, Background=1, NLEVELS=levels, Color=Black
MAP_GRID, /BOX_AXES, COLOR=255
MAP_CONTINENTS, COLOR=255
;The following coding is to generate color bar if your already download and install Dr. Fanning's Coyote Library and add the coyote directory above. 
;If you don't need color bar in your plot, you can ignore this step and add comment character ';' ahead of coding.
COLORBAR, Range=[datamin,datamax], Ncolors=levels, /Vertical, Position=[0.9,0.1,0.94,0.9]



