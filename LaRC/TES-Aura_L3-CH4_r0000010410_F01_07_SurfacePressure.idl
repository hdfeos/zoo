;  This example code illustrates how to access and visualize 
;  HDF-EOS5 TES L3 Grid file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).

; Define file, grid, and field name.
file_name='TES-Aura_L3-CH4_r0000010410_F01_07.he5'
datafield_name='/HDFEOS/GRIDS/NadirGrid/Data Fields/SurfacePressure'

; Open file.
file_id=H5F_OPEN(file_name)

; Open dataset.
data_id=H5D_OPEN(file_id,datafield_name)

data=H5D_READ(data_id)

; Transpose data and make it be consistent with world map.
data=transpose(data)

; Retrieve lat and lon.
Lat_NAME='/HDFEOS/GRIDS/NadirGrid/Data Fields/Latitude'
lat_id=H5D_OPEN(file_id,Lat_NAME)
lat=H5D_READ(lat_id)

Lon_NAME='/HDFEOS/GRIDS/NadirGrid/Data Fields/Longitude'
lon_id=H5D_OPEN(file_id,Lon_NAME)
lon=H5D_READ(lon_id)

; Get units.
units_id=H5A_OPEN_NAME(data_id, 'Units')
units=H5A_READ(units_id)

; Get missing value.
missingvalue_id=H5A_OPEN_NAME(data_id,'MissingValue')
missingvalue=H5A_READ(missingvalue_id)
H5A_CLOSE, missingvalue_id

; Get title.
title_id=H5A_OPEN_NAME(data_id,'Title')
title=H5A_READ(title_id)
H5A_CLOSE, title_id

; Convert data type.
dataf=FLOAT(data)

; Close data set and file id.
H5D_CLOSE, data_id
H5F_CLOSE, file_id

; Process fill value.
idx=WHERE(data EQ missingvalue(0), cnt)
if cnt gt 0 then dataf[idx]=!Values.F_NAN

; Get min and max value of data.
datamin=min(dataf, /NAN)
datamax=max(dataf, /NAN)

; Generate the plot.
levels = 254
DEVICE, DECOMPOSED=0
LoadCT, 33, NCOLORS=levels, BOTTOM=1
WINDOW, TITLE='FIELD:' + title + '       UNIT:'+units
MAP_SET, /GRID, /CONTINENTS, XMARGIN=5, YMARGIN=5, $
  POSITION=[0.05, 0.05, 0.82, 0.82]
CONTOUR, dataf, lon, lat, /OVERPLOT, /CELL_FILL, C_Colors=Indgen(levels)+3, $
         Background=1, NLEVELS=levels, Color=Black
MAP_GRID, /BOX
MAP_CONTINENTS

; Draw title and unit.
XYOuts, 0.05, 0.86, /Normal, $
        'FIELD:' + title, $
        Charsize=1.25, color=black, Alignment=0.0
XYOuts, 0.95, 0.86, /Normal, 'UNIT:' + units, $ 
  Charsize=1.25, Color=black, Alignment=1.0
XYOuts, 0.5, 0.94, /Normal, file_name, $
  Charsize=1.25, Color=black, Alignment=0.5

;   The following code is prepared for colorbar. 
;
;   If you require colorbar in your plot, you could download 
; "Dr. Fanning's Coyote Library" from [1]. Make a directory named
; coyote somewhere on your machine, and extract the Coyote files into
; it. 
;
;   If color bar is not not necessary for your plot, you can ignore this
; step and add comment character ';' ahead of coding. 
;
;    Add the coyote directory you create on your machine to your IDL
;  path like below: 
;
; !PATH=Expand_Path('[coyote directory on your machine])+':'+!PATH

; Let's assume that you've installed coyote library under the current working
; directory that this code exists.
!PATH=Expand_Path('+coyote/')+':'+!PATH


;  The following code assumes that you've already download and installed 
; "Dr. Fanning's Coyote Library" and add the coyote directory above. 
;
;  If you don't need color bar in your plot, you can ignore this step
;  by adding comment character ';' at the beginning of the code.
COLORBAR, Range=[datamin, datamax], Ncolors=levels, /Vertical, $ 
  Position=[0.9,0.05,0.94,0.8]

; Write JPEG image file.
im = TVRD(TRUE=3)
jpg = 'TES-Aura_L3-CH4_r0000010410_F01_07_SurfacePressure.idl.JPG' 
WRITE_JPEG, jpg, im, QUALITY=100, TRUE=3

; References
;
; [1] http://www.dfanning.com/documents/programs.html     
