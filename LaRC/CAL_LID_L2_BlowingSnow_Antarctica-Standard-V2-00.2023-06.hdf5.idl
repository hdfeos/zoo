;
; This example code illustrates how to access and visualize LaRC
; CALIPSO L2 HDF5 Swath file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
; If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
;
; Usage:
;
;  $idl CAL_LID_L2_BlowingSnow_Antarctica-Standard-V2-00.2023-06.hdf5.idl
;
; Tested under: IDL 8.9.0
; Last updated: 2024-02-08

; Open file.
file_name = 'CAL_LID_L2_BlowingSnow_Antarctica-Standard-V2-00.2023-06.hdf5'
file_id = H5F_OPEN(file_name)

datafield_name = '/Snow_Fields/Blowing_Snow_Depol_Profile'
data_id = H5D_OPEN(file_id, datafield_name)
data = H5D_READ(data_id)

; Get units attribute.
units_id = H5A_OPEN_NAME(data_id, 'units')
units = H5A_READ(units_id)
H5A_Close, units_id

; Set long_name attribute.
long_name = 'Blowing_Snow_Depol_Profile'

; Convert data type.
dataf = float(data)
fillvaluef = 0.0
H5D_CLOSE, data_id

; Read latitude.
latitude_name='/Geolocation_Fields/Latitude'
latitude_id=H5D_OPEN(file_id, latitude_name)
lat=H5D_READ(latitude_id)

; Close dataset.
H5D_CLOSE, latitude_id

; Read longitude.
lon_name='/Geolocation_Fields/Longitude'
lon_id=H5D_OPEN(file_id, lon_name)
lon=H5D_READ(lon_id)
dimsize=size(lon,/dim)

; Close dataset.
H5D_CLOSE, lon_id

; Subset data
dataf = dataf[0,*]
dimsize=size(dataf,/dim)

; Process missing value, convert dataf that are equal to fillvaluef to NaN
idx = where(dataf eq fillvaluef, cnt)
if cnt gt 0 then dataf[idx] = !Values.F_NAN
if cnt gt 0 then lat[idx] = !Values.F_NAN
if cnt gt 0 then lon[idx] = !Values.F_NAN

; Pick only valid points. Otherwise, scatter plot will be wrong.
gidx = WHERE(FINITE(dataf), count)
lats = lat[gidx]
lons = lon[gidx]
datas = dataf[gidx]
datamin = MIN(datas)
datamax = MAX(datas)

m = MAP('Orthographic', TITLE=file_name, FONT_SIZE=9, /BUFFER, $
        CENTER_LONGITUDE = 0,  CENTER_LATITUDE = -90)
ct = COLORTABLE(72, /reverse)
; ct = COLORTABLE(13)
t1 = TEXT(0.35, 0.01, long_name)
c1 = SCATTERPLOT(lons[*], lats[*], OVERPLOT=m, $
                 MAGNITUDE=datas[*], $
                 RGB_TABLE=ct, $
                 POSITION=[0.1, 0.1, 0.83, 0.9], $
                 /SYM_FILLED, SYMBOL='o', SYM_SIZE=0.1)
mc = MAPCONTINENTS()
cb = COLORBAR(RGB_TABLE=ct, RANGE=[datamin, datamax], /BORDER, ORIENTATION=1, $
              TEXTPOS=1, POSITION=[0.85,0.2,0.87,0.8], TITLE=units)

png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT
