;
;  This example code illustrates how to access and visualize LP DAAC AST_08 
;  HDF-EOS2 Swath file in IDL. 
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
; 
; Usage: 
;                               
; $idl AST_08_00310162018232146_20181017215729_30106.hdf.idl
;
; Tested under: IDL 8.6.0
; Last updated: 2018-11-05

; Open file.
file_name='AST_08_00310162018232146_20181017215729_30106.hdf'
file_id=EOS_SW_OPEN(file_name)


; Attach Swath.
swath_name='SurfaceKineticTemperature'
swath_id=EOS_SW_ATTACH(file_id, swath_name)

; Retrieve data.
datafield_name='KineticTemperature'
status=EOS_SW_READFIELD(swath_id,datafield_name,data)

; Retrieve dimension info for title of the plot.
status=EOS_SW_FIELDINFO(swath_id,datafield_name,rank,dims,numbertype,dimlist)

; Detach Swath.
status=EOS_SW_DETACH(swath_id)

; Close file.
status=EOS_SW_CLOSE(file_id)



; Convert data type and apply scale [1].
dataf=float(data) * 0.1


; Get max and min value of data for color bar.
datamin = MIN(dataf, /NAN)
datamax = MAX(dataf, /NAN)

; We need to use eosdump to generate lat and lon.
; To properly display the data, the latitude/longitude must be
; remapped.

; Retrieve dimension size of data.
dimsize=SIZE(dataf,/dim)
numlat=dimsize(0)
numlon=dimsize(1)
size=numlat*numlon

; Read eosdump output file for latitude.
; $eos2dump -a1 AST_08_00310162018232146_20181017215729_30106.hdf SurfaceKineticTemperature > lat_AST_08_00310162018232146_20181017215729_30106.output
OPENR, 1, $
  'lat_AST_08_00310162018232146_20181017215729_30106.output'
lat_input=FLTARR(size)
READF, 1, lat_input

lat=FINDGEN(numlat,numlon)
lat=REFORM(lat_input,numlat,numlon)

; Read eosdump output file for longitude.
; $eos2dump -a2 AST_08_00310162018232146_20181017215729_30106.hdf SurfaceKineticTemperature > lon_AST_08_00310162018232146_20181017215729_30106.output
OPENR, 2, $
  'lon_AST_08_00310162018232146_20181017215729_30106.output'
lon_input=FLTARR(size)
READF, 2, lon_input
lon=FINDGEN(numlat,numlon)
lon=REFORM(lon_input,numlat,numlon)

; Get min/max value of lat and lon for zoomed image.
latmin=MIN(lat)
latmax=MAX(lat)
lonmin=MIN(lon)
lonmax=MAX(lon)


; Generate a plot.
c_lon = lon[numlat/2, numlon/2]
c_lat = lat[numlat/2, numlon/2]

; Get min/max value of lat and lon for zoomed image.
latmin=MIN(lat)
latmax=MAX(lat)
lonmin=MIN(lon)
lonmax=MAX(lon)

; Use LIMIT for zoom-in effect.
m = MAP('Orthographic', TITLE=file_name, FONT_SIZE=9, /BUFFER, $
        CENTER_LONGITUDE = c_lon,  CENTER_LATITUDE = c_lat, $
        LIMIT=[latmin, lonmin, latmax, lonmax])
ct = COLORTABLE(72, /reverse)
t1 = TEXT(0.01, 0.05, FONT_SIZE=8, datafield_name)
c1 = SCATTERPLOT(lon[*], lat[*], OVERPLOT=m, $
                 MAGNITUDE = dataf[*], $
                 RGB_TABLE=ct, $
                 POSITION=[0.1, 0.1, 0.83, 0.9],$
                 /SYM_FILLED, SYMBOL='o', SYM_SIZE=0.1)
mc = MAPCONTINENTS()
units = 'K'
cb = COLORBAR(TARGET=c1, /BORDER, ORIENTATION=1, TEXTPOS=1,$
              RANGE=[datamin, datamax], POSITION=[0.85,0.2,0.87,0.8], TITLE=units)
png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT

; References
;
; [1] https://asterweb.jpl.nasa.gov/content/03_data/01_Data_Products/release_surface_kinetic_temperatur.htm
