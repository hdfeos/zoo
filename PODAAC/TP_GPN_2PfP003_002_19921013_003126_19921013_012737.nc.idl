;
;  This example code illustrates how to access and visualize
;  PO.DAAC TOPEX_POSEIDON_GDR_F MODIS L2 netCDF-4 file in IDL.
;
; If you have any questions, suggestions, comments  on this example,
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
;
; Usage:
;
;  $idl TP_GPN_2PfP003_002_19921013_003126_19921013_012737.nc.idl
;
; Tested under: IDL 8.9.0
; Last updated: 2023-11-09

; Open file. 
file_name='TP_GPN_2PfP003_002_19921013_003126_19921013_012737.nc'
file_id=NCDF_OPEN(file_name)

; Read data.
datafield_name='swh_ku'
data_id=NCDF_VARID(file_id, datafield_name)

; Retrieve data.
NCDF_VARGET, file_id, data_id, data

; Get long_name.
NCDF_ATTGET, file_id, data_id, 'long_name', long_name

; Get units.
NCDF_ATTGET, file_id, data_id, 'units', units

; Get _FillValue.
NCDF_ATTGET, file_id, data_id, '_FillValue', fillvalue

; Get scale.
NCDF_ATTGET, file_id, data_id, 'scale_factor', scale

; Read latitude.
latitude_name='latitude'
latitude_id=NCDF_VARID(file_id, latitude_name)
NCDF_VARGET, file_id, latitude_id, lat

; Get scale.
NCDF_ATTGET, file_id, latitude_id, 'scale_factor', scale_lat

; Read longitude.
lon_name='longitude'
longitude_id=NCDF_VARID(file_id, lon_name)
NCDF_VARGET, file_id, longitude_id, lon

; Get scale.
NCDF_ATTGET, file_id, longitude_id, 'scale_factor', scale_lon

; Close file.
NCDF_CLOSE, file_id

; Convert type to double.
dataf = float(data)

; Process fill value.
idx=where(data eq fillvalue(0), cnt)
if cnt gt 0 then dataf[idx] = !Values.F_NAN

; Process scale/offset.
dataf = scale * dataf
lat = scale_lat * lat
lon = scale_lon * lon
lon = ((lon + 180) MOD 360) - 180

datamin = MIN(dataf, /NAN)
datamax = MAX(dataf, /NAN)

; Generate the plot.
m = MAP('Geographic', TITLE=file_name, FONT_SIZE=9, /BUFFER)
ct = COLORTABLE(72, /reverse)
c1 = SCATTERPLOT(lon, lat, OVERPLOT=m, $
                 MAGNITUDE=dataf, $
                 RGB_TABLE=ct, $
                 POSITION=[0.1, 0.1, 0.83, 0.9],$
                 /SYM_FILLED, SYMBOL='o', SYM_SIZE=0.1)
mc = MAPCONTINENTS()
t1 = TEXT(0.01, 0.05, FONT_SIZE=8, STRING([long_name]))
cb = COLORBAR(RGB_TABLE=ct, RANGE=[datamin, datamax], ORIENTATION=1, BORDER=1,$
              TEXTPOS=1, POSITION=[0.85,0.2,0.87,0.8], TITLE=STRING([units]))
png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT
