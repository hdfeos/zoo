;
;  This example code illustrates how to access and visualize ASF
;  S1-GUNW L3 netCDF-4/HDF5 file in IDL.
;
; If you have any questions, suggestions, comments  on this example, 
; please use the HDF-EOS Forum (http://hdfeos.org/forums).
;
;  If you would like to see an  example of any other NASA HDF/HDF-EOS 
; data product that is not listed in the HDF-EOS Comprehensive Examples page
; (http://hdfeos.org/zoo), feel free to contact us at eoshelp@hdfgroup.org
; or post it at the HDF-EOS Forum (http://hdfeos.org/forums).
;
; Usage:
;
;  $idl S1-GUNW-A-R-072-tops-20230520_20220618-145958-00044E_00030N-PP-ab21-v2_0_6.nc.idl
;
; Tested under: IDL 8.8.3
; Last updated: 2023-07-10

; Open file. 
FILE_NAME='S1-GUNW-A-R-072-tops-20230520_20220618-145958-00044E_00030N-PP-ab21-v2_0_6.nc'

file_id=H5F_OPEN(FILE_NAME)

; Define data field.
DATAFIELD_NAME='/science/grids/data/amplitude'
data_id=H5D_OPEN(file_id, DATAFIELD_NAME)
data=H5D_READ(data_id)

; Get 'units' attribute.
units_id=H5A_OPEN_NAME(data_id, 'units')
units=H5A_READ(units_id)
H5A_CLOSE, units_id

; Get 'long_name' attribute.
longname_id=H5A_OPEN_NAME(data_id, 'long_name')
long_name=H5A_READ(longname_id)
H5A_CLOSE, longname_id

; Get '_FillValue' attribute.
fv_id=H5A_OPEN_NAME(data_id, '_FillValue')
fv=H5A_READ(fv_id)
H5A_CLOSE, fv_id

; Close dataset.
H5D_CLOSE, data_id

; Retrieve lat/lon.
lat_name='/science/grids/data/latitude'
lat_id=H5D_OPEN(file_id, lat_name)
lat=H5D_READ(lat_id)
H5D_CLOSE, lat_id

lon_name='/science/grids/data/longitude'
lon_id=H5D_OPEN(file_id, lon_name)
lon=H5D_READ(lon_id)
H5D_CLOSE, lon_id

; Close file.
H5F_CLOSE, file_id

; Process missing value, convert data that are equal to missingvaluef to NaN
;dataf = FLOAT(data)
idx = where(data eq fv(0), cnt)
if cnt gt 0 then data[idx] = !Values.F_NAN

; Get min/max value of lat and lon for zoomed image.
latmin=MIN(lat)
latmax=MAX(lat)
lonmin=MIN(lon)
lonmax=MAX(lon)

; Generate the plot.
m = MAP('Geographic', LIMIT=[latmin, lonmin, latmax, lonmax], $
	TITLE=file_name, FONT_SIZE=9, /BUFFER)
ct = COLORTABLE(72, /reverse)
; Sample every 50th point. 
c1 = CONTOUR(data[0:*:50, 0:*:50], lon[0:*:50], lat[0:*:50], $
             /FILL, OVERPLOT=m, $
             RGB_TABLE=ct, $
             GRID_UNITS='degrees', POSITION=[0.1, 0.1, 0.83, 0.9])
mc = MAPCONTINENTS()
cb = COLORBAR(TARGET=c1, /BORDER, ORIENTATION=1, TEXTPOS=1, POSITION=[0.85,0.2,0.87,0.8], TITLE=units)
t1 = TEXT(0.01, 0.05, FONT_SIZE=8, long_name)
; Save image.
png = file_name + '.idl.png'
c1.save, png, HEIGHT=600, WIDTH=800
EXIT

